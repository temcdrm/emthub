# deletes everything
DROP ALL

# enumerates all classes found; see appendix of Learning SPARQL for more useful cookbook recipes
SELECT ?class (COUNT(?class) as ?cnt)
WHERE {
  ?s a ?class .
} group by ?class order by ?class

# discover the attributes of each class
SELECT DISTINCT ?class ?attr 
WHERE {
  ?s a ?class .
  ?s ?attr ?val
} ORDER BY ?class ?attr

# lists all triples; should be none after clearing all circuits
SELECT ?s ?attr ?val where {
  ?s ?attr ?val.
} order by ?s ?attr

##################################################


PREFIX r:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX c:  <http://www.ucaiug.org/ns#>
PREFIX e:  <http://opensource.ieee.org/emtiop#>
#PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

##################################################

# EMTContainer
# IEEE39	6477751A-0472-4FD6-B3C3-3AD4945CBE56
# IEEE118	1783D2A8-1204-4781-A0B4-7A73A2FA6038
# WECC240	2540AF5C-4F83-4C0F-9577-DEE8CC73BBB3
# XfmrSat       93EA6BF1-A569-4190-9590-98A62780489E
SELECT ?name ?id WHERE {
 ?s r:type c:EquipmentContainer.
 ?s c:IdentifiedObject.name ?name.
 ?s c:IdentifiedObject.mRID ?id.
}
ORDER by ?name

# EMTDisconnectingCircuitBreaker
SELECT ?name ?bus1 ?bus2 ?basev ?id ?sysid ?cn1id ?cn2id WHERE {
 ?s r:type c:DisconnectingCircuitBreaker.
# VALUES ?sysid {"6477751A-0472-4FD6-B3C3-3AD4945CBE56"^^c:String}
# VALUES ?sysid {"1783D2A8-1204-4781-A0B4-7A73A2FA6038"^^c:String}
# VALUES ?sysid {"2540AF5C-4F83-4C0F-9577-DEE8CC73BBB3"^^c:String}
 VALUES ?sysid {"93EA6BF1-A569-4190-9590-98A62780489E"^^c:String}
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?s c:Equipment.EquipmentContainer ?sys.
 ?s c:IdentifiedObject.name ?name.
 ?s c:IdentifiedObject.mRID ?id. 
 ?s c:ConductingEquipment.BaseVoltage ?bv.
 ?bv c:BaseVoltage.nominalVoltage ?basev.
 ?s e:ConductingEquipment.FromConnectivityNode ?cn1.
 ?s e:ConductingEquipment.ToConnectivityNode ?cn2.
 ?cn1 c:IdentifiedObject.mRID ?cn1id.
 ?cn1 c:IdentifiedObject.name ?bus1.
 ?cn2 c:IdentifiedObject.mRID ?cn2id.
 ?cn2 c:IdentifiedObject.name ?bus2
}
ORDER by ?name

# EMTEnergySource
SELECT ?name ?id ?bus ?basev ?r ?x ?r0 ?x0 ?nomv ?vmag ?vang ?sysid ?cn1id WHERE {
# VALUES ?sysid {"1783D2A8-1204-4781-A0B4-7A73A2FA6038"^^c:String}
# VALUES ?sysid {"2540AF5C-4F83-4C0F-9577-DEE8CC73BBB3"^^c:String}
 VALUES ?sysid {"93EA6BF1-A569-4190-9590-98A62780489E"^^c:String}
 ?s r:type c:EnergySource.
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?s c:Equipment.EquipmentContainer ?sys.
 ?s c:IdentifiedObject.name ?name.
 ?s c:IdentifiedObject.mRID ?id.
 ?s c:ConductingEquipment.BaseVoltage ?bv.
 ?bv c:BaseVoltage.nominalVoltage ?basev.
 ?s c:EnergySource.r ?r.
 ?s c:EnergySource.x ?x.
 ?s c:EnergySource.r0 ?r0.
 ?s c:EnergySource.x0 ?x0.
 ?s c:EnergySource.nominalVoltage ?nomv.
 ?s c:EnergySource.voltageMagnitude ?vmag.
 ?s c:EnergySource.voltageAngle ?vang.
 ?s e:ConductingEquipment.FromConnectivityNode ?cn. 
 ?cn c:IdentifiedObject.mRID ?cn1id. 
 ?cn c:IdentifiedObject.name ?bus
} ORDER by ?name

# EMTBus
SELECT DISTINCT ?name ?id ?nomv WHERE {
 VALUES ?sysid {"1783D2A8-1204-4781-A0B4-7A73A2FA6038"^^c:String}
# VALUES ?sysid {"2540AF5C-4F83-4C0F-9577-DEE8CC73BBB3"^^c:String}
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?s c:ConnectivityNode.ConnectivityNodeContainer ?sys.
 ?s r:type c:ConnectivityNode.
 ?s c:IdentifiedObject.name ?name.
 ?s c:IdentifiedObject.mRID ?id.
 { 
   ?ce c:Equipment.EquipmentContainer ?sys.
   ?ce e:ConductingEquipment.FromConnectivityNode ?s.
   ?ce c:ConductingEquipment.BaseVoltage ?bv.
 } UNION { 
   ?ce c:Equipment.EquipmentContainer ?sys.
   ?ce e:ConductingEquipment.ToConnectivityNode ?s.
   ?ce c:ConductingEquipment.BaseVoltage ?bv.
 } UNION {
   ?pxf c:Equipment.EquipmentContainer ?sys.
   ?end c:PowerTransformerEnd.PowerTransformer ?pxf.
   ?end e:TransformerEnd.ConnectivityNode ?s.
   ?end c:TransformerEnd.BaseVoltage ?bv.
 }
 ?bv c:BaseVoltage.nominalVoltage ?nomv.
}
ORDER by ?name

# EMTBusXY
SELECT DISTINCT ?name ?id ?text ?cnid ?seq ?x ?y WHERE {
 VALUES ?sysid {"1783D2A8-1204-4781-A0B4-7A73A2FA6038"^^c:String}
# VALUES ?sysid {"2540AF5C-4F83-4C0F-9577-DEE8CC73BBB3"^^c:String}
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?s c:ConnectivityNode.ConnectivityNodeContainer ?sys.
 ?s r:type c:ConnectivityNode.
 ?s c:IdentifiedObject.mRID ?cnid.
 ?tdo c:DiagramObject.IdentifiedObject ?s.
 ?tdo c:TextDiagramObject.text ?text.
 ?tdo c:IdentifiedObject.name ?name.
 ?tdo c:IdentifiedObject.mRID ?id.
 ?pt c:DiagramObjectPoint.DiagramObject ?tdo.
 ?pt c:DiagramObjectPoint.sequenceNumber ?seq.
 ?pt c:DiagramObjectPoint.xPosition ?x.
 ?pt c:DiagramObjectPoint.yPosition ?y.
}
ORDER by ?name ?seq

# EMTBaseVoltage
SELECT DISTINCT ?vnom WHERE {
# VALUES ?sysid {"1783D2A8-1204-4781-A0B4-7A73A2FA6038"^^c:String}
# VALUES ?sysid {"2540AF5C-4F83-4C0F-9577-DEE8CC73BBB3"^^c:String}
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?s c:Equipment.EquipmentContainer ?sys.
 {?s c:ConductingEquipment.BaseVoltage ?lev.}
  UNION
 { ?end c:PowerTransformerEnd.PowerTransformer ?s.
   ?end c:TransformerEnd.BaseVoltage ?lev.}
 ?lev r:type c:BaseVoltage.
 ?lev c:BaseVoltage.nominalVoltage ?vstr.
  bind(xsd:string(?vstr) as ?xsdstr)
  bind(xsd:float(?xsdstr) as ?vnom)
}
ORDER BY ?vnom

# EMTLine
SELECT ?name ?id ?basev ?bus1 ?bus2 ?len ?r ?x ?b ?r0 ?x0 ?b0 ?sysid ?cn1id ?cn2id WHERE {
 ?s r:type c:ACLineSegment.
 VALUES ?sysid {"1783D2A8-1204-4781-A0B4-7A73A2FA6038"^^c:String}
# VALUES ?sysid {"2540AF5C-4F83-4C0F-9577-DEE8CC73BBB3"^^c:String}
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?s c:Equipment.EquipmentContainer ?sys.
 ?s c:IdentifiedObject.mRID ?id.
 ?s c:IdentifiedObject.name ?name.
 ?s c:ConductingEquipment.BaseVoltage ?bv.
 ?bv c:BaseVoltage.nominalVoltage ?basev.
 ?s c:Conductor.length ?len.
 ?s c:ACLineSegment.r ?r.
 ?s c:ACLineSegment.x ?x.
 ?s c:ACLineSegment.bch ?b.
 ?s c:ACLineSegment.r0 ?r0.
 ?s c:ACLineSegment.x0 ?x0.
 ?s c:ACLineSegment.b0ch ?b0.
 ?s e:ConductingEquipment.FromConnectivityNode ?cn1.
 ?s e:ConductingEquipment.ToConnectivityNode ?cn2.
 ?cn1 c:IdentifiedObject.mRID ?cn1id.
 ?cn1 c:IdentifiedObject.name ?bus1.
 ?cn2 c:IdentifiedObject.mRID ?cn2id.
 ?cn2 c:IdentifiedObject.name ?bus2
}
ORDER BY ?name

# EMTLoad
SELECT ?name ?id ?bus ?basev ?p ?q ?pz ?qz ?pi ?qi ?pp ?qp ?pe ?qe ?sysid ?cn1id WHERE {
 ?s r:type c:EnergyConsumer.
 VALUES ?sysid {"1783D2A8-1204-4781-A0B4-7A73A2FA6038"^^c:String}
# VALUES ?sysid {"2540AF5C-4F83-4C0F-9577-DEE8CC73BBB3"^^c:String}
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?s c:Equipment.EquipmentContainer ?sys.
 ?s c:IdentifiedObject.name ?name.
 ?s c:IdentifiedObject.mRID ?id.
 ?s c:ConductingEquipment.BaseVoltage ?bv.
 ?bv c:BaseVoltage.nominalVoltage ?basev.
 ?s c:EnergyConsumer.p ?p.
 ?s c:EnergyConsumer.q ?q.
 ?s c:EnergyConsumer.LoadResponse ?lr.
 ?lr c:LoadResponseCharacteristic.pConstantImpedance ?pz.
 ?lr c:LoadResponseCharacteristic.qConstantImpedance ?qz.
 ?lr c:LoadResponseCharacteristic.pConstantCurrent ?pi.
 ?lr c:LoadResponseCharacteristic.qConstantCurrent ?qi.
 ?lr c:LoadResponseCharacteristic.pConstantPower ?pp.
 ?lr c:LoadResponseCharacteristic.qConstantPower ?qp.
 OPTIONAL {?lr c:LoadResponseCharacteristic.pVoltageExponent ?pe.}
 OPTIONAL {?lr c:LoadResponseCharacteristic.qVoltageExponent ?qe.}
 ?s e:ConductingEquipment.FromConnectivityNode ?cn. 
 ?cn c:IdentifiedObject.mRID ?cn1id. 
 ?cn c:IdentifiedObject.name ?bus
}
ORDER by ?name

# EMTCountPowerXfmrWinding
SELECT ?id (count(?p) as ?count) WHERE {
# VALUES ?sysid {"1783D2A8-1204-4781-A0B4-7A73A2FA6038"^^c:String}
 VALUES ?sysid {"2540AF5C-4F83-4C0F-9577-DEE8CC73BBB3"^^c:String}
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?p c:Equipment.EquipmentContainer ?sys.
 ?p r:type c:PowerTransformer.
 ?p c:IdentifiedObject.mRID ?id.
 ?end c:PowerTransformerEnd.PowerTransformer ?p.
}
GROUP BY ?id
ORDER BY ?id

# EMTPowerXfmrWinding
SELECT ?id ?pname ?vgrp ?enum ?bus ?basev ?conn ?ratedS ?ratedU ?ang ?grounded ?rground ?xground ?cn1id ?pid WHERE {
 VALUES ?sysid {"1783D2A8-1204-4781-A0B4-7A73A2FA6038"^^c:String}
# VALUES ?sysid {"2540AF5C-4F83-4C0F-9577-DEE8CC73BBB3"^^c:String}
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?p c:Equipment.EquipmentContainer ?sys.
 ?p r:type c:PowerTransformer.
 ?p c:IdentifiedObject.name ?pname.
 ?p c:IdentifiedObject.mRID ?pid.
 ?p c:PowerTransformer.vectorGroup ?vgrp.
 ?end c:PowerTransformerEnd.PowerTransformer ?p.
 ?end c:TransformerEnd.endNumber ?enum.
 ?end c:PowerTransformerEnd.ratedS ?ratedS.
 ?end c:PowerTransformerEnd.ratedU ?ratedU.
 ?end c:PowerTransformerEnd.phaseAngleClock ?ang.
 ?end c:PowerTransformerEnd.connectionKind ?connraw.  
  bind(strafter(str(?connraw),"WindingConnection.") as ?conn)
 ?end c:TransformerEnd.grounded ?grounded.
 OPTIONAL {?end c:TransformerEnd.rground ?rground.}
 OPTIONAL {?end c:TransformerEnd.xground ?xground.}
 ?end c:IdentifiedObject.mRID ?id.
 ?end e:TransformerEnd.ConnectivityNode ?cn. 
 ?cn c:IdentifiedObject.mRID ?cn1id. 
 ?cn c:IdentifiedObject.name ?bus.
 ?end c:TransformerEnd.BaseVoltage ?bv.
 ?bv c:BaseVoltage.nominalVoltage ?basev
}
ORDER BY ?pname ?enum

# EMTPowerXfmrCore
SELECT ?pname ?enum ?b ?g ?id ?eid ?pid WHERE {
 VALUES ?sysid {"1783D2A8-1204-4781-A0B4-7A73A2FA6038"^^c:String}
# VALUES ?sysid {"2540AF5C-4F83-4C0F-9577-DEE8CC73BBB3"^^c:String}
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?p c:Equipment.EquipmentContainer ?sys.
 ?p r:type c:PowerTransformer.
 ?p c:IdentifiedObject.name ?pname.
 ?end c:PowerTransformerEnd.PowerTransformer ?p.
 ?adm c:TransformerCoreAdmittance.TransformerEnd ?end.
 ?end c:TransformerEnd.endNumber ?enum.
 ?adm c:TransformerCoreAdmittance.b ?b.
 ?adm c:TransformerCoreAdmittance.g ?g.
 ?p c:IdentifiedObject.mRID ?pid.
 ?end c:IdentifiedObject.mRID ?eid.
 ?adm c:IdentifiedObject.mRID ?id
}
ORDER BY ?pname

# EMTXfmrTap
SELECT ?pname ?enum ?svi ?step ?eid WHERE {
 VALUES ?sysid {"6477751A-0472-4FD6-B3C3-3AD4945CBE56"^^c:String}
# VALUES ?sysid {"1783D2A8-1204-4781-A0B4-7A73A2FA6038"^^c:String}
# VALUES ?sysid {"2540AF5C-4F83-4C0F-9577-DEE8CC73BBB3"^^c:String}
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?p c:Equipment.EquipmentContainer ?sys.
 ?p c:IdentifiedObject.name ?pname.
 ?rtc r:type c:RatioTapChanger.
 ?rtc c:RatioTapChanger.TransformerEnd ?end.
 ?end c:PowerTransformerEnd.PowerTransformer ?p.
 ?end c:TransformerEnd.endNumber ?enum.
 ?rtc c:RatioTapChanger.stepVoltageIncrement ?svi.
 ?rtc c:TapChanger.step ?step.
 ?end c:IdentifiedObject.mRID ?eid
}
ORDER BY ?pname ?enum

# EMTPowerXfmrMesh
SELECT ?pname ?fnum ?tnum ?r ?x ?pid ?fid ?tid ?id WHERE {
 VALUES ?sysid {"1783D2A8-1204-4781-A0B4-7A73A2FA6038"^^c:String}
# VALUES ?sysid {"2540AF5C-4F83-4C0F-9577-DEE8CC73BBB3"^^c:String}
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?p c:Equipment.EquipmentContainer ?sys.
 ?p r:type c:PowerTransformer.
 ?p c:IdentifiedObject.name ?pname.
 ?from c:PowerTransformerEnd.PowerTransformer ?p.
 ?imp c:TransformerMeshImpedance.FromTransformerEnd ?from.
 ?imp c:TransformerMeshImpedance.ToTransformerEnd ?to.
 ?imp c:TransformerMeshImpedance.r ?r.
 ?imp c:TransformerMeshImpedance.x ?x.
 ?from c:TransformerEnd.endNumber ?fnum.
 ?to c:TransformerEnd.endNumber ?tnum.
 ?p c:IdentifiedObject.mRID ?pid.
 ?from c:IdentifiedObject.mRID ?fid.
 ?to c:IdentifiedObject.mRID ?tid.
 ?imp c:IdentifiedObject.mRID ?id
}
ORDER BY ?pname ?fnum ?tnum

# EMTXfmrSaturation
SELECT ?pname ?pid ?admid ?amps ?vs WHERE {
# VALUES ?sysid {"1783D2A8-1204-4781-A0B4-7A73A2FA6038"^^c:String}
 VALUES ?sysid {"2540AF5C-4F83-4C0F-9577-DEE8CC73BBB3"^^c:String}
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?p c:Equipment.EquipmentContainer ?sys.
 ?p r:type c:PowerTransformer.
 ?p c:IdentifiedObject.name ?pname.
 ?p c:IdentifiedObject.mRID ?pid.
 ?end c:PowerTransformerEnd.PowerTransformer ?p.
 ?adm c:TransformerCoreAdmittance.TransformerEnd ?end.
 ?adm c:IdentifiedObject.mRID ?admid.
 ?sat e:TransformerSaturation.TransformerCoreAdmittance ?adm.
 ?crv c:CurveData.Curve ?sat.
 ?crv c:CurveData.xvalue ?xvalue.
 ?crv c:CurveData.y1value ?y1value.
  bind(xsd:string(?xvalue) as ?xstr)
  bind(xsd:float(?xstr) as ?amps)
  bind(xsd:string(?y1value) as ?ystr)
  bind(xsd:float(?ystr) as ?vs)
}
ORDER BY ?pid ?amps ?vs

# EMTCompShunt may be capacitor or reactor
SELECT ?name ?basev ?nomu ?gsection ?bsection ?sections ?maxsections ?bus ?id ?sysid ?cn1id WHERE {
 ?s r:type c:LinearShuntCompensator.
# VALUES ?sysid {"1783D2A8-1204-4781-A0B4-7A73A2FA6038"^^c:String}
 VALUES ?sysid {"2540AF5C-4F83-4C0F-9577-DEE8CC73BBB3"^^c:String}
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?s c:Equipment.EquipmentContainer ?sys.
 ?s c:IdentifiedObject.name ?name.
 ?s c:ConductingEquipment.BaseVoltage ?bv.
 ?bv c:BaseVoltage.nominalVoltage ?basev.
 ?s c:ShuntCompensator.nomU ?nomu. 
 ?s c:LinearShuntCompensator.gPerSection ?gsection. 
 ?s c:LinearShuntCompensator.bPerSection ?bsection. 
 ?s c:ShuntCompensator.sections ?sections. 
 ?s c:ShuntCompensator.maximumSections ?maxsections. 
 ?s c:IdentifiedObject.mRID ?id. 
 ?s e:ConductingEquipment.FromConnectivityNode ?cn. 
 ?cn c:IdentifiedObject.mRID ?cn1id.
 ?cn c:IdentifiedObject.name ?bus
}
ORDER by ?name

# EMTCompSeries may be capacitor or reactor
SELECT ?name ?bus1 ?bus2 ?basev ?r ?r0 ?x ?x0 ?id ?sysid ?cn1id ?cn2id WHERE {
 ?s r:type c:SeriesCompensator.
# VALUES ?sysid {"1783D2A8-1204-4781-A0B4-7A73A2FA6038"^^c:String}
 VALUES ?sysid {"2540AF5C-4F83-4C0F-9577-DEE8CC73BBB3"^^c:String}
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?s c:Equipment.EquipmentContainer ?sys.
 ?s c:IdentifiedObject.name ?name.
 ?s c:IdentifiedObject.mRID ?id. 
 ?s c:ConductingEquipment.BaseVoltage ?bv.
 ?bv c:BaseVoltage.nominalVoltage ?basev.
 ?s c:SeriesCompensator.r ?r. 
 ?s c:SeriesCompensator.r0 ?r0. 
 ?s c:SeriesCompensator.x ?x. 
 ?s c:SeriesCompensator.x0 ?x0. 
 ?s e:ConductingEquipment.FromConnectivityNode ?cn1.
 ?s e:ConductingEquipment.ToConnectivityNode ?cn2.
 ?cn1 c:IdentifiedObject.mRID ?cn1id.
 ?cn1 c:IdentifiedObject.name ?bus1.
 ?cn2 c:IdentifiedObject.mRID ?cn2id.
 ?cn2 c:IdentifiedObject.name ?bus2
}
ORDER by ?name

# EMTSyncMachine
SELECT ?name ?type ?bus ?ratedS ?ratedU ?p ?q ?maxP ?minP ?maxQ ?minQ
       ?inertia ?Xd ?Xdp ?Xdpp ?Xq ?Xqp ?Xqpp ?Ra ?Xl
       ?Tdop ?Tdopp ?Tqop ?Tqopp ?id ?uid ?sysid ?cn1id WHERE {
# VALUES ?sysid {"1783D2A8-1204-4781-A0B4-7A73A2FA6038"^^c:String}
 VALUES ?sysid {"2540AF5C-4F83-4C0F-9577-DEE8CC73BBB3"^^c:String}
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?s r:type c:SynchronousMachine.
 ?s c:Equipment.EquipmentContainer ?sys.
 ?s c:IdentifiedObject.name ?name.
 ?s c:IdentifiedObject.mRID ?id.
 ?s c:RotatingMachine.ratedS ?ratedS.
 ?s c:RotatingMachine.ratedU ?ratedU.
 ?s c:RotatingMachine.p ?p.
 ?s c:RotatingMachine.q ?q.
 ?s c:RotatingMachine.GeneratingUnit ?unit.
 ?unit c:GeneratingUnit.maxOperatingP ?maxP. 
 ?unit c:GeneratingUnit.minOperatingP ?minP.
 ?unit c:IdentifiedObject.mRID ?uid.
 {?unit a ?rawtype.
  bind(strafter(str(?rawtype),"#") as ?postfix)
  bind(strbefore(str(?postfix),"GeneratingUnit") as ?type)}
 ?s c:SynchronousMachine.maxQ ?maxQ.
 ?s c:SynchronousMachine.minQ ?minQ.
 ?dyn c:SynchronousMachineDynamics.SynchronousMachine ?s.
 ?dyn c:RotatingMachineDynamics.inertia ?inertia.
 ?dyn c:RotatingMachineDynamics.statorResistance ?Ra.
 ?dyn c:RotatingMachineDynamics.statorLeakageReactance ?Xl.
 ?dyn c:SynchronousMachineTimeConstantReactance.xDirectSync ?Xd.
 ?dyn c:SynchronousMachineTimeConstantReactance.xDirectTrans ?Xdp.
 ?dyn c:SynchronousMachineTimeConstantReactance.xDirectSubtrans ?Xdpp.
 ?dyn c:SynchronousMachineTimeConstantReactance.xQuadSync ?Xq.
 ?dyn c:SynchronousMachineTimeConstantReactance.xQuadTrans ?Xqp.
 ?dyn c:SynchronousMachineTimeConstantReactance.xQuadSubtrans ?Xqpp.
 ?dyn c:SynchronousMachineTimeConstantReactance.tpdo ?Tdop.
 ?dyn c:SynchronousMachineTimeConstantReactance.tppdo ?Tdopp.
 ?dyn c:SynchronousMachineTimeConstantReactance.tpqo ?Tqop.
 ?dyn c:SynchronousMachineTimeConstantReactance.tppqo ?Tqopp.
 ?s e:ConductingEquipment.FromConnectivityNode ?cn.
 ?cn c:IdentifiedObject.mRID ?cn1id.
 ?cn c:IdentifiedObject.name ?bus
}
ORDER by ?name

# EMTSolar
SELECT ?name ?bus ?ratedS ?ratedU ?maxP ?minP ?maxQ ?minQ ?ipu ?p ?q ?sysid ?pecid ?id ?cn1id WHERE {
# VALUES ?sysid {"1783D2A8-1204-4781-A0B4-7A73A2FA6038"^^c:String}
 VALUES ?sysid {"2540AF5C-4F83-4C0F-9577-DEE8CC73BBB3"^^c:String}
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?s r:type c:PhotoVoltaicUnit.
 ?s c:IdentifiedObject.name ?name.
 ?s c:IdentifiedObject.mRID ?id.
 ?s c:PowerElectronicsUnit.maxP ?maxP.
 ?s c:PowerElectronicsUnit.minP ?minP.
 ?s c:PowerElectronicsUnit.PowerElectronicsConnection ?pec.
 ?pec c:Equipment.EquipmentContainer ?sys.
 ?pec c:PowerElectronicsConnection.ratedS ?ratedS.
 ?pec c:PowerElectronicsConnection.ratedU ?ratedU.
 ?pec c:PowerElectronicsConnection.maxIFault ?ipu.
 ?pec c:PowerElectronicsConnection.p ?p.
 ?pec c:PowerElectronicsConnection.q ?q.
 ?pec c:PowerElectronicsConnection.minQ ?minQ.
 ?pec c:PowerElectronicsConnection.maxQ ?maxQ.
 ?pec c:IdentifiedObject.mRID ?pecid.
 ?pec e:ConductingEquipment.FromConnectivityNode ?cn. 
 ?cn c:IdentifiedObject.mRID ?cn1id.
 ?cn c:IdentifiedObject.name ?bus
}
ORDER by ?name

# EMTWind
SELECT ?name ?bus ?ratedS ?ratedU ?maxP ?minP ?maxQ ?minQ ?ipu ?p ?q ?sysid ?pecid ?id ?cn1id WHERE {
 VALUES ?sysid {"1783D2A8-1204-4781-A0B4-7A73A2FA6038"^^c:String}
# VALUES ?sysid {"2540AF5C-4F83-4C0F-9577-DEE8CC73BBB3"^^c:String}
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?s r:type c:PowerElectronicsWindUnit.
 ?s c:IdentifiedObject.name ?name.
 ?s c:IdentifiedObject.mRID ?id.
 ?s c:PowerElectronicsUnit.maxP ?maxP.
 ?s c:PowerElectronicsUnit.minP ?minP.
 ?s c:PowerElectronicsUnit.PowerElectronicsConnection ?pec.
 ?pec c:Equipment.EquipmentContainer ?sys.
 ?pec c:PowerElectronicsConnection.ratedS ?ratedS.
 ?pec c:PowerElectronicsConnection.ratedU ?ratedU.
 ?pec c:PowerElectronicsConnection.maxIFault ?ipu.
 ?pec c:PowerElectronicsConnection.p ?p.
 ?pec c:PowerElectronicsConnection.q ?q.
 ?pec c:PowerElectronicsConnection.minQ ?minQ.
 ?pec c:PowerElectronicsConnection.maxQ ?maxQ.
 ?pec c:IdentifiedObject.mRID ?pecid.
 ?pec e:ConductingEquipment.FromConnectivityNode ?cn. 
 ?cn c:IdentifiedObject.mRID ?cn1id.
 ?cn c:IdentifiedObject.name ?bus
}
ORDER by ?name

# EMTGovSteamSGO
SELECT ?name ?k1 ?k2 ?k3 ?pmin ?pmax ?t1 ?t2 ?t3 ?t4 ?t5 ?t6 ?mwbase ?id ?eqid ?sysid
WHERE {
# VALUES ?sysid {"1783D2A8-1204-4781-A0B4-7A73A2FA6038"^^c:String}
 VALUES ?sysid {"2540AF5C-4F83-4C0F-9577-DEE8CC73BBB3"^^c:String}
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?s r:type c:GovSteamSGO.
 ?s c:TurbineGovernorDynamics.SynchronousMachineDynamics ?dyn.
 ?dyn c:SynchronousMachineDynamics.SynchronousMachine ?eq.
 ?eq c:Equipment.EquipmentContainer ?sys.
 ?eq c:IdentifiedObject.mRID ?eqid.
 ?s r:type c:GovSteamSGO.
 ?s c:IdentifiedObject.name ?name.
 ?s c:IdentifiedObject.mRID ?id.
 ?s c:GovSteamSGO.k1 ?k1.
 ?s c:GovSteamSGO.k2 ?k2.
 ?s c:GovSteamSGO.k3 ?k3.
 ?s c:GovSteamSGO.pmin ?pmin.
 ?s c:GovSteamSGO.pmax ?pmax.
 ?s c:GovSteamSGO.t1 ?t1.
 ?s c:GovSteamSGO.t2 ?t2.
 ?s c:GovSteamSGO.t3 ?t3.
 ?s c:GovSteamSGO.t4 ?t4.
 ?s c:GovSteamSGO.t5 ?t5.
 ?s c:GovSteamSGO.t6 ?t6.
 ?s c:GovSteamSGO.mwbase ?mwbase.
} ORDER BY ?name

# EMTExcST1A
SELECT ?name ?ilr ?ka ?kc ?kf ?klr ?ta ?tb ?tb1 ?tc ?tc1 ?tf ?vamax ?vamin
?vimax ?vimin ?vrmax ?vrmin ?xe ?id ?eqid ?sysid WHERE {
# VALUES ?sysid {"1783D2A8-1204-4781-A0B4-7A73A2FA6038"^^c:String}
 VALUES ?sysid {"2540AF5C-4F83-4C0F-9577-DEE8CC73BBB3"^^c:String}
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?s r:type c:ExcST1A.
 ?s c:ExcitationSystemDynamics.SynchronousMachineDynamics ?dyn.
 ?dyn c:SynchronousMachineDynamics.SynchronousMachine ?eq.
 ?eq c:Equipment.EquipmentContainer ?sys.
 ?eq c:IdentifiedObject.mRID ?eqid.
 ?s r:type c:ExcST1A.
 ?s c:IdentifiedObject.name ?name.
 ?s c:IdentifiedObject.mRID ?id.
 ?s c:ExcST1A.ilr ?ilr.
 ?s c:ExcST1A.ka ?ka.
 ?s c:ExcST1A.kc ?kc.
 ?s c:ExcST1A.kf ?kf.
 ?s c:ExcST1A.klr ?klr.
 ?s c:ExcST1A.ta ?ta.
 ?s c:ExcST1A.tb ?tb.
 ?s c:ExcST1A.tb1 ?tb1.
 ?s c:ExcST1A.tc ?tc.
 ?s c:ExcST1A.tc1 ?tc1.
 ?s c:ExcST1A.tf ?tf.
 ?s c:ExcST1A.vamax ?vamax.
 ?s c:ExcST1A.vamin ?vamin.
 ?s c:ExcST1A.vimax ?vimax.
 ?s c:ExcST1A.vimin ?vimin.
 ?s c:ExcST1A.vrmax ?vrmax.
 ?s c:ExcST1A.vrmin ?vrmin.
 ?s c:ExcST1A.xe ?xe.
} ORDER BY ?name

# EMTPssIEEE1A
SELECT ?name ?a1 ?a2 ?inputSignalType ?ks ?t1 ?t2 ?t3 ?t4 ?t5 ?t6 ?vrmax ?vrmin
?id ?eqid ?sysid WHERE {
# VALUES ?sysid {"1783D2A8-1204-4781-A0B4-7A73A2FA6038"^^c:String}
 VALUES ?sysid {"2540AF5C-4F83-4C0F-9577-DEE8CC73BBB3"^^c:String}
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?s r:type c:PssIEEE1A.
 ?s c:PowerSystemStabilizerDynamics.ExcitationSystemDynamics ?exc.
 ?exc c:ExcitationSystemDynamics.SynchronousMachineDynamics ?dyn.
 ?dyn c:SynchronousMachineDynamics.SynchronousMachine ?eq.
 ?eq c:Equipment.EquipmentContainer ?sys.
 ?eq c:IdentifiedObject.mRID ?eqid.
 ?s r:type c:PssIEEE1A.
 ?s c:IdentifiedObject.name ?name.
 ?s c:IdentifiedObject.mRID ?id.
 ?s c:PssIEEE1A.a1 ?a1.
 ?s c:PssIEEE1A.a2 ?a2.
 ?s c:PssIEEE1A.inputSignalType ?inputSignalRaw.
    bind(strafter(str(?inputSignalRaw),"InputSignalKind.") as ?inputSignalType)
 ?s c:PssIEEE1A.ks ?ks.
 ?s c:PssIEEE1A.t1 ?t1.
 ?s c:PssIEEE1A.t2 ?t2.
 ?s c:PssIEEE1A.t3 ?t3.
 ?s c:PssIEEE1A.t4 ?t4.
 ?s c:PssIEEE1A.t5 ?t5.
 ?s c:PssIEEE1A.t6 ?t6.
 ?s c:PssIEEE1A.vrmax ?vrmax.
 ?s c:PssIEEE1A.vrmin ?vrmin.
} ORDER BY ?name

# EMTWeccREGCA
SELECT ?name ?brkpt ?iolim ?iqrmax ?iqrmin ?ivpl1 ?ivplsw ?ivpnt0 ?ivpnt1 ?khv
?rrpwr ?tfltr ?tg ?volim ?zerox ?id ?eqid ?sysid WHERE {
# VALUES ?sysid {"1783D2A8-1204-4781-A0B4-7A73A2FA6038"^^c:String}
 VALUES ?sysid {"2540AF5C-4F83-4C0F-9577-DEE8CC73BBB3"^^c:String}
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?s r:type c:WeccREGCA.
 ?s c:WeccDynamics.PowerElectronicsConnection ?eq.
 ?eq c:Equipment.EquipmentContainer ?sys.
 ?eq c:IdentifiedObject.mRID ?eqid.
 ?s r:type c:WeccREGCA.
 ?s c:IdentifiedObject.name ?name.
 ?s c:IdentifiedObject.mRID ?id.
 ?s c:WeccREGCA.brkpt ?brkpt.
 ?s c:WeccREGCA.iolim ?iolim.
 ?s c:WeccREGCA.iqrmax ?iqrmax.
 ?s c:WeccREGCA.iqrmin ?iqrmin.
 ?s c:WeccREGCA.ivpl1 ?ivpl1.
 ?s c:WeccREGCA.ivplsw ?ivplsw.
 ?s c:WeccREGCA.ivpnt0 ?ivpnt0.
 ?s c:WeccREGCA.ivpnt1 ?ivpnt1.
 ?s c:WeccREGCA.khv ?khv.
 ?s c:WeccREGCA.rrpwr ?rrpwr.
 ?s c:WeccREGCA.tfltr ?tfltr.
 ?s c:WeccREGCA.tg ?tg.
 ?s c:WeccREGCA.volim ?volim.
 ?s c:WeccREGCA.zerox ?zerox.
} ORDER BY ?name

# EMTWeccREECA
SELECT ?name ?db1 ?db2 ?dPmax ?dPmin ?imax ?iqfrz ?iqh1 ?iql1 ?kqi ?kqp ?kqv
?kvi ?kvp ?pfFlag ?pFlag ?pmax ?pmin ?pqFlag ?qFlag ?qmax ?qmin ?thld ?thld2
?tiq ?tp ?tpord ?trv ?vdi1i1 ?vdi1i2 ?vdi1i3 ?vdi1i4 ?vdi1v1 ?vdi1v2 ?vdi1v3
?vdi1v4 ?vdi2i1 ?vdi2i2 ?vdi2i3 ?vdi2i4 ?vdi2v1 ?vdi2v2 ?vdi2v3 ?vdi2v4 ?vdip
?vFlag ?vmax ?vmin ?vref0 ?vref1 ?vup ?id ?eqid ?sysid WHERE {
# VALUES ?sysid {"1783D2A8-1204-4781-A0B4-7A73A2FA6038"^^c:String}
 VALUES ?sysid {"2540AF5C-4F83-4C0F-9577-DEE8CC73BBB3"^^c:String}
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?s r:type c:WeccREECA.
 ?s c:WeccDynamics.PowerElectronicsConnection ?eq.
 ?eq c:Equipment.EquipmentContainer ?sys.
 ?eq c:IdentifiedObject.mRID ?eqid.
 ?s r:type c:WeccREECA.
 ?s c:IdentifiedObject.name ?name.
 ?s c:IdentifiedObject.mRID ?id.
 ?s c:WeccREECA.db1 ?db1.
 ?s c:WeccREECA.db2 ?db2.
 ?s c:WeccREECA.dPmax ?dPmax.
 ?s c:WeccREECA.dPmin ?dPmin.
 ?s c:WeccREECA.imax ?imax.
 ?s c:WeccREECA.iqfrz ?iqfrz.
 ?s c:WeccREECA.iqh1 ?iqh1.
 ?s c:WeccREECA.iql1 ?iql1.
 ?s c:WeccREECA.kqi ?kqi.
 ?s c:WeccREECA.kqp ?kqp.
 ?s c:WeccREECA.kqv ?kqv.
 ?s c:WeccREECA.kvi ?kvi.
 ?s c:WeccREECA.kvp ?kvp.
 ?s c:WeccREECA.pfFlag ?pfFlag.
 ?s c:WeccREECA.pFlag ?pFlag.
 ?s c:WeccREECA.pmax ?pmax.
 ?s c:WeccREECA.pmin ?pmin.
 ?s c:WeccREECA.pqFlag ?pqFlag.
 ?s c:WeccREECA.qFlag ?qFlag.
 ?s c:WeccREECA.qmax ?qmax.
 ?s c:WeccREECA.qmin ?qmin.
 ?s c:WeccREECA.thld ?thld.
 ?s c:WeccREECA.thld2 ?thld2.
 ?s c:WeccREECA.tiq ?tiq.
 ?s c:WeccREECA.tp ?tp.
 ?s c:WeccREECA.tpord ?tpord.
 ?s c:WeccREECA.trv ?trv.
 ?s c:WeccREECA.vdi1i1 ?vdi1i1.
 ?s c:WeccREECA.vdi1i2 ?vdi1i2.
 ?s c:WeccREECA.vdi1i3 ?vdi1i3.
 ?s c:WeccREECA.vdi1i4 ?vdi1i4.
 ?s c:WeccREECA.vdi1v1 ?vdi1v1.
 ?s c:WeccREECA.vdi1v2 ?vdi1v2.
 ?s c:WeccREECA.vdi1v3 ?vdi1v3.
 ?s c:WeccREECA.vdi1v4 ?vdi1v4.
 ?s c:WeccREECA.vdi2i1 ?vdi2i1.
 ?s c:WeccREECA.vdi2i2 ?vdi2i2.
 ?s c:WeccREECA.vdi2i3 ?vdi2i3.
 ?s c:WeccREECA.vdi2i4 ?vdi2i4.
 ?s c:WeccREECA.vdi2v1 ?vdi2v1.
 ?s c:WeccREECA.vdi2v2 ?vdi2v2.
 ?s c:WeccREECA.vdi2v3 ?vdi2v3.
 ?s c:WeccREECA.vdi2v4 ?vdi2v4.
 ?s c:WeccREECA.vdip ?vdip.
 ?s c:WeccREECA.vFlag ?vFlag.
 ?s c:WeccREECA.vmax ?vmax.
 ?s c:WeccREECA.vmin ?vmin.
 ?s c:WeccREECA.vref0 ?vref0.
 ?s c:WeccREECA.vref1 ?vref1.
 ?s c:WeccREECA.vup ?vup.
} ORDER BY ?name

# EMTWeccREPCA
SELECT ?name ?db ?ddn ?dup ?emax ?emin ?fdbd1 ?fdbd2 ?femax ?femin ?frqFlag ?kc
?ki ?kig ?kp ?kpg ?pmax ?pmin ?qmax ?qmin ?rc ?refFlag ?tfltr ?tft ?tfv ?tlag
?tp ?vcmpFlag ?vfrz ?xc ?id ?eqid ?sysid WHERE {
# VALUES ?sysid {"1783D2A8-1204-4781-A0B4-7A73A2FA6038"^^c:String}
 VALUES ?sysid {"2540AF5C-4F83-4C0F-9577-DEE8CC73BBB3"^^c:String}
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?s r:type c:WeccREPCA.
 ?s c:WeccDynamics.PowerElectronicsConnection ?eq.
 ?eq c:Equipment.EquipmentContainer ?sys.
 ?eq c:IdentifiedObject.mRID ?eqid.
 ?s r:type c:WeccREPCA.
 ?s c:IdentifiedObject.name ?name.
 ?s c:IdentifiedObject.mRID ?id.
 ?s c:WeccREPCA.db ?db.
 ?s c:WeccREPCA.ddn ?ddn.
 ?s c:WeccREPCA.dup ?dup.
 ?s c:WeccREPCA.emax ?emax.
 ?s c:WeccREPCA.emin ?emin.
 ?s c:WeccREPCA.fdbd1 ?fdbd1.
 ?s c:WeccREPCA.fdbd2 ?fdbd2.
 ?s c:WeccREPCA.femax ?femax.
 ?s c:WeccREPCA.femin ?femin.
 ?s c:WeccREPCA.frqFlag ?frqFlag.
 ?s c:WeccREPCA.kc ?kc.
 ?s c:WeccREPCA.ki ?ki.
 ?s c:WeccREPCA.kig ?kig.
 ?s c:WeccREPCA.kp ?kp.
 ?s c:WeccREPCA.kpg ?kpg.
 ?s c:WeccREPCA.pmax ?pmax.
 ?s c:WeccREPCA.pmin ?pmin.
 ?s c:WeccREPCA.qmax ?qmax.
 ?s c:WeccREPCA.qmin ?qmin.
 ?s c:WeccREPCA.rc ?rc.
 ?s c:WeccREPCA.refFlag ?refFlag.
 ?s c:WeccREPCA.tfltr ?tfltr.
 ?s c:WeccREPCA.tft ?tft.
 ?s c:WeccREPCA.tfv ?tfv.
 ?s c:WeccREPCA.tlag ?tlag.
 ?s c:WeccREPCA.tp ?tp.
 ?s c:WeccREPCA.vcmpFlag ?vcmpFlag.
 ?s c:WeccREPCA.vfrz ?vfrz.
 ?s c:WeccREPCA.xc ?xc.
} ORDER BY ?name

# EMTWeccWTGTA
SELECT ?name ?dshaft ?hg ?ht ?kshaft ?id ?eqid ?sysid WHERE {
# VALUES ?sysid {"1783D2A8-1204-4781-A0B4-7A73A2FA6038"^^c:String}
 VALUES ?sysid {"2540AF5C-4F83-4C0F-9577-DEE8CC73BBB3"^^c:String}
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?s r:type c:WeccWTGTA.
 ?s c:WeccDynamics.PowerElectronicsConnection ?eq.
 ?eq c:Equipment.EquipmentContainer ?sys.
 ?eq c:IdentifiedObject.mRID ?eqid.
 ?s r:type c:WeccWTGTA.
 ?s c:IdentifiedObject.name ?name.
 ?s c:IdentifiedObject.mRID ?id.
 ?s c:WeccWTGTA.dshaft ?dshaft.
 ?s c:WeccWTGTA.hg ?hg.
 ?s c:WeccWTGTA.ht ?ht.
 ?s c:WeccWTGTA.kshaft ?kshaft.
} ORDER BY ?name

# EMTWeccWTGARA
SELECT ?name ?ka ?t0 ?id ?eqid ?sysid WHERE {
# VALUES ?sysid {"1783D2A8-1204-4781-A0B4-7A73A2FA6038"^^c:String}
 VALUES ?sysid {"2540AF5C-4F83-4C0F-9577-DEE8CC73BBB3"^^c:String}
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?s r:type c:WeccWTGARA.
 ?s c:WeccDynamics.PowerElectronicsConnection ?eq.
 ?eq c:Equipment.EquipmentContainer ?sys.
 ?eq c:IdentifiedObject.mRID ?eqid.
 ?s r:type c:WeccWTGARA.
 ?s c:IdentifiedObject.name ?name.
 ?s c:IdentifiedObject.mRID ?id.
 ?s c:WeccWTGARA.ka ?ka.
 ?s c:WeccWTGARA.t0 ?t0.
} ORDER BY ?name

# EMTXfmrLimit
SELECT DISTINCT ?id ?name ?endid ?tname ?value ?dur ?inf ?dir WHERE {
# VALUES ?sysid {"6477751A-0472-4FD6-B3C3-3AD4945CBE56"^^c:String}
# VALUES ?sysid {"1783D2A8-1204-4781-A0B4-7A73A2FA6038"^^c:String}
# VALUES ?sysid {"2540AF5C-4F83-4C0F-9577-DEE8CC73BBB3"^^c:String}
 VALUES ?sysid {"93EA6BF1-A569-4190-9590-98A62780489E"^^c:String}
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?p c:Equipment.EquipmentContainer ?sys.
 ?end c:PowerTransformerEnd.PowerTransformer ?p.
 ?end c:IdentifiedObject.mRID ?endid.
 ?ols e:OperationalLimitSet.TransformerEnd ?end.
 ?s c:OperationalLimit.OperationalLimitSet ?ols.
 ?s c:OperationalLimit.OperationalLimitType ?olt.
 ?s r:type c:ApparentPowerLimit.
 ?s c:IdentifiedObject.mRID ?id.
 ?s c:IdentifiedObject.name ?name.
 ?s c:ApparentPowerLimit.value ?valstr.
  bind(xsd:string(?valstr) as ?xsdstr)
  bind(xsd:float(?xsdstr) as ?value)
 ?olt c:IdentifiedObject.name ?tname.
 ?olt c:OperationalLimitType.isInfiniteDuration ?inf.
 ?olt c:OperationalLimitType.direction ?rawdir.
  bind(strafter(str(?rawdir), "OperationalLimitDirectionKind.") as ?dir)
 OPTIONAL {?olt c:OperationalLimitType.acceptableDuration ?dur.}
}
ORDER BY ?endid ?inf ?tname ?inf ?value

# EMTBranchLimit
SELECT DISTINCT ?id ?name ?eqid ?eqtype ?tname ?value ?dur ?inf ?dir WHERE {
# VALUES ?sysid {"6477751A-0472-4FD6-B3C3-3AD4945CBE56"^^c:String}
# VALUES ?sysid {"1783D2A8-1204-4781-A0B4-7A73A2FA6038"^^c:String}
# VALUES ?sysid {"2540AF5C-4F83-4C0F-9577-DEE8CC73BBB3"^^c:String}
 VALUES ?sysid {"93EA6BF1-A569-4190-9590-98A62780489E"^^c:String}
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?eq c:Equipment.EquipmentContainer ?sys.
 ?eq c:IdentifiedObject.mRID ?eqid.
 ?eq r:type ?typeraw.
  bind(strafter(str(?typeraw),"#") as ?eqtype)
 ?ols e:OperationalLimitSet.ConductingEquipment ?eq.
 ?s c:OperationalLimit.OperationalLimitSet ?ols.
 ?s c:OperationalLimit.OperationalLimitType ?olt.
 ?s r:type c:ApparentPowerLimit.
 ?s c:IdentifiedObject.mRID ?id.
 ?s c:IdentifiedObject.name ?name.
 ?s c:ApparentPowerLimit.value ?valstr.
  bind(xsd:string(?valstr) as ?xsdstr)
  bind(xsd:float(?xsdstr) as ?value)
 ?olt c:IdentifiedObject.name ?tname.
 ?olt c:OperationalLimitType.isInfiniteDuration ?inf.
 ?olt c:OperationalLimitType.direction ?rawdir.
  bind(strafter(str(?rawdir), "OperationalLimitDirectionKind.") as ?dir)
 OPTIONAL {?olt c:OperationalLimitType.acceptableDuration ?dur.}
}
ORDER BY ?eqid ?inf ?tname ?inf ?value

# EMTBusVoltage
SELECT DISTINCT ?cnid ?name ?vmag ?vdeg WHERE {
# VALUES ?sysid {"6477751A-0472-4FD6-B3C3-3AD4945CBE56"^^c:String}
# VALUES ?sysid {"1783D2A8-1204-4781-A0B4-7A73A2FA6038"^^c:String}
# VALUES ?sysid {"2540AF5C-4F83-4C0F-9577-DEE8CC73BBB3"^^c:String}
 VALUES ?sysid {"93EA6BF1-A569-4190-9590-98A62780489E"^^c:String}
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?s c:ConnectivityNode.ConnectivityNodeContainer ?sys.
 ?s c:IdentifiedObject.mRID ?cnid.
 ?s c:IdentifiedObject.name ?name.
 ?sv e:SvVoltage.ConnectivityNode ?s.
 ?sv c:SvVoltage.v ?vmag.
 ?sv c:SvVoltage.angle ?vdeg
}
ORDER BY ?name

# EMTBranchFlow
SELECT DISTINCT ?eqid ?eqtype ?eqname ?p ?q WHERE {
# VALUES ?sysid {"6477751A-0472-4FD6-B3C3-3AD4945CBE56"^^c:String}
# VALUES ?sysid {"1783D2A8-1204-4781-A0B4-7A73A2FA6038"^^c:String}
# VALUES ?sysid {"2540AF5C-4F83-4C0F-9577-DEE8CC73BBB3"^^c:String}
 VALUES ?sysid {"93EA6BF1-A569-4190-9590-98A62780489E"^^c:String}
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?s c:Equipment.EquipmentContainer ?sys.
 ?s c:IdentifiedObject.mRID ?eqid.
 ?s c:IdentifiedObject.name ?eqname.
 ?s r:type ?typeraw.
  bind(strafter(str(?typeraw),"#") as ?eqtype)
 ?sv e:SvPowerFlow.ConductingEquipment ?s.
 ?sv c:SvPowerFlow.p ?p.
 ?sv c:SvPowerFlow.q ?q
}
ORDER BY ?eqtype ?eqname

# EMTXfmrFlow
SELECT DISTINCT ?endid ?endtype ?endname ?p ?q WHERE {
# VALUES ?sysid {"6477751A-0472-4FD6-B3C3-3AD4945CBE56"^^c:String}
# VALUES ?sysid {"1783D2A8-1204-4781-A0B4-7A73A2FA6038"^^c:String}
# VALUES ?sysid {"2540AF5C-4F83-4C0F-9577-DEE8CC73BBB3"^^c:String}
 VALUES ?sysid {"93EA6BF1-A569-4190-9590-98A62780489E"^^c:String}
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?pxf c:Equipment.EquipmentContainer ?sys.
 ?end c:PowerTransformerEnd.PowerTransformer ?pxf.
 ?end c:IdentifiedObject.mRID ?endid.
 ?end c:IdentifiedObject.name ?endname.
 ?end r:type ?typeraw.
  bind(strafter(str(?typeraw),"#") as ?endtype)
 ?sv e:SvPowerFlow.TransformerEnd ?end.
 ?sv c:SvPowerFlow.p ?p.
 ?sv c:SvPowerFlow.q ?q
}
ORDER BY ?endtype ?endname

# EMTBusVoltageIC
SELECT DISTINCT ?cnid ?vmag ?vdeg WHERE {
 ?sv e:SvVoltage.ConnectivityNode ?cn.
  bind(substr(str(?cn),strlen(str(?cn))-35) as ?cnid)
 ?sv c:SvVoltage.v ?vmag.
 ?sv c:SvVoltage.angle ?vdeg
} ORDER BY ?cnid

# EMTBranchFlowIC
SELECT DISTINCT ?eqid ?p ?q WHERE {
 ?sv e:SvPowerFlow.ConductingEquipment ?eq.
  bind(substr(str(?eq),strlen(str(?eq))-35) as ?eqid)
 ?sv c:SvPowerFlow.p ?p.
 ?sv c:SvPowerFlow.q ?q
} ORDER BY ?eqid

# EMTXfmrFlowIC
SELECT DISTINCT ?endid ?p ?q WHERE {
 ?sv e:SvPowerFlow.TransformerEnd ?end.
  bind(substr(str(?end),strlen(str(?end))-35) as ?endid)
 ?sv c:SvPowerFlow.p ?p.
 ?sv c:SvPowerFlow.q ?q
} ORDER BY ?endid

# EMTPlantGSU
SELECT DISTINCT ?name ?type ?bus ?rm_or_pec_id ?xfid ?vgroup ?enum ?endid WHERE {
 VALUES ?sysid {"6477751A-0472-4FD6-B3C3-3AD4945CBE56"^^c:String}
# VALUES ?sysid {"1783D2A8-1204-4781-A0B4-7A73A2FA6038"^^c:String}
# VALUES ?sysid {"2540AF5C-4F83-4C0F-9577-DEE8CC73BBB3"^^c:String}
# VALUES ?sysid {"93EA6BF1-A569-4190-9590-98A62780489E"^^c:String}
 ?sys c:IdentifiedObject.mRID ?sysid.
# ?s r:type c:SynchronousMachine.
 ?s c:Equipment.EquipmentContainer ?sys.
 ?s c:IdentifiedObject.name ?name.
 ?s c:IdentifiedObject.mRID ?rm_or_pec_id.
 {?s c:RotatingMachine.GeneratingUnit ?unit.}
  UNION
 {?unit c:PowerElectronicsUnit.PowerElectronicsConnection ?s.}
 {?unit a ?rawtype.
  bind(strafter(str(?rawtype),"#") as ?type)}
 ?s e:ConductingEquipment.FromConnectivityNode ?cn.
 ?cn c:IdentifiedObject.mRID ?cn1id.
 ?cn c:IdentifiedObject.name ?bus.
 ?end e:TransformerEnd.ConnectivityNode ?cn.
 ?end c:IdentifiedObject.mRID ?endid.
 ?end c:PowerTransformerEnd.PowerTransformer ?pxf.
 ?end c:TransformerEnd.endNumber ?enum.
 ?pxf c:IdentifiedObject.mRID ?xfid.
 ?pxf c:PowerTransformer.vectorGroup ?vgroup
}
ORDER by ?name

# EMTRotatingMachinePlant
SELECT DISTINCT ?id ?name ?eqid ?eqtype WHERE {
 ?p r:type e:RotatingMachinePlant.
 ?p c:IdentifiedObject.name ?name.
 ?p c:IdentifiedObject.mRID ?id.
 ?p e:GeneratingPlant.Equipments ?eq.
 ?eq c:IdentifiedObject.mRID ?eqid.
 {?eq a ?rawtype.
  bind(strafter(str(?rawtype),"#") as ?eqtype)}
} ORDER by ?id ?eqtype ?eqid

# EMTIBRPlant
SELECT DISTINCT ?id ?name ?eqid ?eqtype WHERE {
 ?p r:type e:IBRPlant.
 ?p c:IdentifiedObject.name ?name.
 ?p c:IdentifiedObject.mRID ?id.
 ?p e:GeneratingPlant.Equipments ?eq.
 ?eq c:IdentifiedObject.mRID ?eqid.
 {?eq a ?rawtype.
  bind(strafter(str(?rawtype),"#") as ?eqtype)}
} ORDER by ?id ?eqtype ?eqid

