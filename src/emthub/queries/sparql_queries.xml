<?xml version="1.0" encoding="UTF-8" ?>
<queries>
<nsCIM>
<![CDATA[
http://www.ucaiug.org/ns#
]]>
</nsCIM>
<nsRDF>
<![CDATA[
http://www.w3.org/1999/02/22-rdf-syntax-ns#
]]>
</nsRDF>
<nsEMT>
<![CDATA[
http://opensource.ieee.org/emtiop#
]]>
</nsEMT>
<query>
<id>EMTBus</id>
<keyfld>id</keyfld>
<description></description>
<value>
<![CDATA[
SELECT DISTINCT ?name ?id ?nomv WHERE {
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?s c:ConnectivityNode.ConnectivityNodeContainer ?sys.
 ?s r:type c:ConnectivityNode.
 ?s c:IdentifiedObject.name ?name.
 ?s c:IdentifiedObject.mRID ?id.
 { 
   ?ce c:Equipment.EquipmentContainer ?sys.
   ?ce e:ConductingEquipment.FromConnectivityNode ?s.
   ?ce c:ConductingEquipment.BaseVoltage ?bv.
 } UNION { 
   ?ce c:Equipment.EquipmentContainer ?sys.
   ?ce e:ConductingEquipment.ToConnectivityNode ?s.
   ?ce c:ConductingEquipment.BaseVoltage ?bv.
 } UNION {
   ?pxf c:Equipment.EquipmentContainer ?sys.
   ?end c:PowerTransformerEnd.PowerTransformer ?pxf.
   ?end e:TransformerEnd.ConnectivityNode ?s.
   ?end c:TransformerEnd.BaseVoltage ?bv.
 }
 ?bv c:BaseVoltage.nominalVoltage ?nomv.
}
ORDER by ?name
]]>
</value>
</query>
<query>
<id>EMTBusXY</id>
<keyfld>id</keyfld>
<description></description>
<value>
<![CDATA[
SELECT DISTINCT ?name ?id ?text ?cnid ?seq ?x ?y WHERE {
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?s c:ConnectivityNode.ConnectivityNodeContainer ?sys.
 ?s r:type c:ConnectivityNode.
 ?s c:IdentifiedObject.mRID ?cnid.
 ?tdo c:DiagramObject.IdentifiedObject ?s.
 ?tdo c:TextDiagramObject.text ?text.
 ?tdo c:IdentifiedObject.name ?name.
 ?tdo c:IdentifiedObject.mRID ?id.
 ?pt c:DiagramObjectPoint.DiagramObject ?tdo.
 ?pt c:DiagramObjectPoint.sequenceNumber ?seq.
 ?pt c:DiagramObjectPoint.xPosition ?x.
 ?pt c:DiagramObjectPoint.yPosition ?y.
}
ORDER by ?name ?seq
]]>
</value>
</query>
<query>
<id>EMTSyncMachine</id>
<keyfld>id</keyfld>
<description></description>
<value>
<![CDATA[
SELECT ?name ?type ?bus ?ratedS ?ratedU ?p ?q ?maxP ?minP ?maxQ ?minQ
       ?inertia ?Xd ?Xdp ?Xdpp ?Xq ?Xqp ?Xqpp ?Ra ?Xl
       ?Tdop ?Tdopp ?Tqop ?Tqopp ?id ?uid ?sysid ?cn1id WHERE {
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?s r:type c:SynchronousMachine.
 ?s c:Equipment.EquipmentContainer ?sys.
 ?s c:IdentifiedObject.name ?name.
 ?s c:IdentifiedObject.mRID ?id.
 ?s c:RotatingMachine.ratedS ?ratedS.
 ?s c:RotatingMachine.ratedU ?ratedU.
 ?s c:RotatingMachine.p ?p.
 ?s c:RotatingMachine.q ?q.
 ?s c:RotatingMachine.GeneratingUnit ?unit.
 ?unit c:GeneratingUnit.maxOperatingP ?maxP. 
 ?unit c:GeneratingUnit.minOperatingP ?minP.
 ?unit c:IdentifiedObject.mRID ?uid.
 {?unit a ?rawtype.
  bind(strafter(str(?rawtype),"#") as ?postfix)
  bind(strbefore(str(?postfix),"GeneratingUnit") as ?type)}
 ?s c:SynchronousMachine.maxQ ?maxQ.
 ?s c:SynchronousMachine.minQ ?minQ.
 ?dyn c:SynchronousMachineDynamics.SynchronousMachine ?s.
 ?dyn c:RotatingMachineDynamics.inertia ?inertia.
 ?dyn c:RotatingMachineDynamics.statorResistance ?Ra.
 ?dyn c:RotatingMachineDynamics.statorLeakageReactance ?Xl.
 ?dyn c:SynchronousMachineTimeConstantReactance.xDirectSync ?Xd.
 ?dyn c:SynchronousMachineTimeConstantReactance.xDirectTrans ?Xdp.
 ?dyn c:SynchronousMachineTimeConstantReactance.xDirectSubtrans ?Xdpp.
 ?dyn c:SynchronousMachineTimeConstantReactance.xQuadSync ?Xq.
 ?dyn c:SynchronousMachineTimeConstantReactance.xQuadTrans ?Xqp.
 ?dyn c:SynchronousMachineTimeConstantReactance.xQuadSubtrans ?Xqpp.
 ?dyn c:SynchronousMachineTimeConstantReactance.tpdo ?Tdop.
 ?dyn c:SynchronousMachineTimeConstantReactance.tppdo ?Tdopp.
 ?dyn c:SynchronousMachineTimeConstantReactance.tpqo ?Tqop.
 ?dyn c:SynchronousMachineTimeConstantReactance.tppqo ?Tqopp.
 ?s e:ConductingEquipment.FromConnectivityNode ?cn.
 ?cn c:IdentifiedObject.mRID ?cn1id.
 ?cn c:IdentifiedObject.name ?bus
}
ORDER by ?name
]]>
</value>
</query>
<query>
<id>EMTLine</id>
<keyfld>id</keyfld>
<description></description>
<value>
<![CDATA[
SELECT ?name ?id ?basev ?bus1 ?bus2 ?len ?r ?x ?b ?r0 ?x0 ?b0 ?sysid ?cn1id ?cn2id WHERE {
 ?s r:type c:ACLineSegment.
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?s c:Equipment.EquipmentContainer ?sys.
 ?s c:IdentifiedObject.mRID ?id.
 ?s c:IdentifiedObject.name ?name.
 ?s c:ConductingEquipment.BaseVoltage ?bv.
 ?bv c:BaseVoltage.nominalVoltage ?basev.
 ?s c:Conductor.length ?len.
 ?s c:ACLineSegment.r ?r.
 ?s c:ACLineSegment.x ?x.
 ?s c:ACLineSegment.bch ?b.
 ?s c:ACLineSegment.r0 ?r0.
 ?s c:ACLineSegment.x0 ?x0.
 ?s c:ACLineSegment.b0ch ?b0.
 ?s e:ConductingEquipment.FromConnectivityNode ?cn1.
 ?s e:ConductingEquipment.ToConnectivityNode ?cn2.
 ?cn1 c:IdentifiedObject.mRID ?cn1id.
 ?cn1 c:IdentifiedObject.name ?bus1.
 ?cn2 c:IdentifiedObject.mRID ?cn2id.
 ?cn2 c:IdentifiedObject.name ?bus2
}
ORDER BY ?name
]]>
</value>
</query>
<query>
<id>EMTDisconnectingCircuitBreaker</id>
<keyfld>id</keyfld>
<description></description>
<value>
<![CDATA[
SELECT ?name ?bus1 ?bus2 ?basev ?id ?sysid ?cn1id ?cn2id WHERE {
 ?s r:type c:DisconnectingCircuitBreaker.
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?s c:Equipment.EquipmentContainer ?sys.
 ?s c:IdentifiedObject.name ?name.
 ?s c:IdentifiedObject.mRID ?id. 
 ?s c:ConductingEquipment.BaseVoltage ?bv.
 ?bv c:BaseVoltage.nominalVoltage ?basev.
 ?s e:ConductingEquipment.FromConnectivityNode ?cn1.
 ?s e:ConductingEquipment.ToConnectivityNode ?cn2.
 ?cn1 c:IdentifiedObject.mRID ?cn1id.
 ?cn1 c:IdentifiedObject.name ?bus1.
 ?cn2 c:IdentifiedObject.mRID ?cn2id.
 ?cn2 c:IdentifiedObject.name ?bus2
}
ORDER by ?name
]]>
</value>
</query>
<query>
<id>EMTEnergySource</id>
<keyfld>id</keyfld>
<description></description>
<value>
<![CDATA[
SELECT ?name ?id ?bus ?basev ?r ?x ?r0 ?x0 ?nomv ?vmag ?vang ?sysid ?cn1id WHERE {
 ?s r:type c:EnergySource.
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?s c:Equipment.EquipmentContainer ?sys.
 ?s c:IdentifiedObject.name ?name.
 ?s c:IdentifiedObject.mRID ?id.
 ?s c:ConductingEquipment.BaseVoltage ?bv.
 ?bv c:BaseVoltage.nominalVoltage ?basev.
 ?s c:EnergySource.r ?r.
 ?s c:EnergySource.x ?x.
 ?s c:EnergySource.r0 ?r0.
 ?s c:EnergySource.x0 ?x0.
 ?s c:EnergySource.nominalVoltage ?nomv.
 ?s c:EnergySource.voltageMagnitude ?vmag.
 ?s c:EnergySource.voltageAngle ?vang.
 ?s e:ConductingEquipment.FromConnectivityNode ?cn. 
 ?cn c:IdentifiedObject.mRID ?cn1id. 
 ?cn c:IdentifiedObject.name ?bus
} ORDER by ?name
]]>
</value>
</query>
<query>
<id>EMTCompSeries</id>
<keyfld>id</keyfld>
<description>series capacitor or reactor</description>
<value>
<![CDATA[
SELECT ?name ?bus1 ?bus2 ?basev ?r ?r0 ?x ?x0 ?id ?sysid ?cn1id ?cn2id WHERE {
 ?s r:type c:SeriesCompensator.
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?s c:Equipment.EquipmentContainer ?sys.
 ?s c:IdentifiedObject.name ?name.
 ?s c:IdentifiedObject.mRID ?id. 
 ?s c:ConductingEquipment.BaseVoltage ?bv.
 ?bv c:BaseVoltage.nominalVoltage ?basev.
 ?s c:SeriesCompensator.r ?r. 
 ?s c:SeriesCompensator.r0 ?r0. 
 ?s c:SeriesCompensator.x ?x. 
 ?s c:SeriesCompensator.x0 ?x0. 
 ?s e:ConductingEquipment.FromConnectivityNode ?cn1.
 ?s e:ConductingEquipment.ToConnectivityNode ?cn2.
 ?cn1 c:IdentifiedObject.mRID ?cn1id.
 ?cn1 c:IdentifiedObject.name ?bus1.
 ?cn2 c:IdentifiedObject.mRID ?cn2id.
 ?cn2 c:IdentifiedObject.name ?bus2
}
ORDER by ?name
]]>
</value>
</query>
<query>
<id>EMTPowerXfmrWinding</id>
<keyfld>pid:enum</keyfld>
<description>Power transformers on their own, without catalog reference.</description>
<value>
<![CDATA[
SELECT ?id ?pname ?vgrp ?enum ?bus ?basev ?conn ?ratedS ?ratedU ?ang ?grounded ?rground ?xground ?cn1id ?pid WHERE {
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?p c:Equipment.EquipmentContainer ?sys.
 ?p r:type c:PowerTransformer.
 ?p c:IdentifiedObject.name ?pname.
 ?p c:IdentifiedObject.mRID ?pid.
 ?p c:PowerTransformer.vectorGroup ?vgrp.
 ?end c:PowerTransformerEnd.PowerTransformer ?p.
 ?end c:TransformerEnd.endNumber ?enum.
 ?end c:PowerTransformerEnd.ratedS ?ratedS.
 ?end c:PowerTransformerEnd.ratedU ?ratedU.
 ?end c:PowerTransformerEnd.phaseAngleClock ?ang.
 ?end c:PowerTransformerEnd.connectionKind ?connraw.  
  bind(strafter(str(?connraw),"WindingConnection.") as ?conn)
 ?end c:TransformerEnd.grounded ?grounded.
 OPTIONAL {?end c:TransformerEnd.rground ?rground.}
 OPTIONAL {?end c:TransformerEnd.xground ?xground.}
 ?end c:IdentifiedObject.mRID ?id.
 ?end e:TransformerEnd.ConnectivityNode ?cn. 
 ?cn c:IdentifiedObject.mRID ?cn1id. 
 ?cn c:IdentifiedObject.name ?bus.
 ?end c:TransformerEnd.BaseVoltage ?bv.
 ?bv c:BaseVoltage.nominalVoltage ?basev
}
ORDER BY ?pname ?enum
]]>
</value>
</query>
<query>
<id>EMTPowerXfmrCore</id>
<keyfld>pid</keyfld>
<description></description>
<value>
<![CDATA[
SELECT ?pname ?enum ?b ?g ?id ?eid ?pid WHERE {
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?p c:Equipment.EquipmentContainer ?sys.
 ?p r:type c:PowerTransformer.
 ?p c:IdentifiedObject.name ?pname.
 ?end c:PowerTransformerEnd.PowerTransformer ?p.
 ?adm c:TransformerCoreAdmittance.TransformerEnd ?end.
 ?end c:TransformerEnd.endNumber ?enum.
 ?adm c:TransformerCoreAdmittance.b ?b.
 ?adm c:TransformerCoreAdmittance.g ?g.
 ?p c:IdentifiedObject.mRID ?pid.
 ?end c:IdentifiedObject.mRID ?eid.
 ?adm c:IdentifiedObject.mRID ?id
}
ORDER BY ?pname
]]>
</value>
</query>
<query>
<id>EMTXfmrTap</id>
<keyfld>eid</keyfld>
<description>Off-nominal tap in percent.</description>
<value>
<![CDATA[
SELECT ?pname ?enum ?svi ?step ?eid WHERE {
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?p c:Equipment.EquipmentContainer ?sys.
 ?p c:IdentifiedObject.name ?pname.
 ?rtc r:type c:RatioTapChanger.
 ?rtc c:RatioTapChanger.TransformerEnd ?end.
 ?end c:PowerTransformerEnd.PowerTransformer ?p.
 ?end c:TransformerEnd.endNumber ?enum.
 ?rtc c:RatioTapChanger.stepVoltageIncrement ?svi.
 ?rtc c:TapChanger.step ?step.
 ?end c:IdentifiedObject.mRID ?eid.
}
ORDER BY ?pname ?enum
]]>
</value>
</query>
<query>
<id>EMTPowerXfmrMesh</id>
<keyfld>fid:tid</keyfld>
<description></description>
<value>
<![CDATA[
SELECT ?pname ?fnum ?tnum ?r ?x ?pid ?fid ?tid ?id WHERE {
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?p c:Equipment.EquipmentContainer ?sys.
 ?p r:type c:PowerTransformer.
 ?p c:IdentifiedObject.name ?pname.
 ?from c:PowerTransformerEnd.PowerTransformer ?p.
 ?imp c:TransformerMeshImpedance.FromTransformerEnd ?from.
 ?imp c:TransformerMeshImpedance.ToTransformerEnd ?to.
 ?imp c:TransformerMeshImpedance.r ?r.
 ?imp c:TransformerMeshImpedance.x ?x.
 ?from c:TransformerEnd.endNumber ?fnum.
 ?to c:TransformerEnd.endNumber ?tnum.
 ?p c:IdentifiedObject.mRID ?pid.
 ?from c:IdentifiedObject.mRID ?fid.
 ?to c:IdentifiedObject.mRID ?tid.
 ?imp c:IdentifiedObject.mRID ?id
}
ORDER BY ?pname ?fnum ?tnum
]]>
</value>
</query>
<query>
<id>EMTXfmrSaturation</id>
<keyfld>pid:amps</keyfld>
<description></description>
<value>
<![CDATA[
SELECT ?pname ?admid ?amps ?vs ?pid WHERE {
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?p c:Equipment.EquipmentContainer ?sys.
 ?p r:type c:PowerTransformer.
 ?p c:IdentifiedObject.name ?pname.
 ?p c:IdentifiedObject.mRID ?pid.
 ?end c:PowerTransformerEnd.PowerTransformer ?p.
 ?adm c:TransformerCoreAdmittance.TransformerEnd ?end.
 ?adm c:IdentifiedObject.mRID ?admid.
 ?sat e:TransformerSaturation.TransformerCoreAdmittance ?adm.
 ?crv c:CurveData.Curve ?sat.
 ?crv c:CurveData.xvalue ?xvalue.
 ?crv c:CurveData.y1value ?y1value.
  bind(xsd:string(?xvalue) as ?xstr)
  bind(xsd:float(?xstr) as ?amps)
  bind(xsd:string(?y1value) as ?ystr)
  bind(xsd:float(?ystr) as ?vs)
}
ORDER BY ?pid ?amps ?vs
]]>
</value>
</query>
<query>
<id>EMTContainer</id>
<keyfld>id</keyfld>
<description>list all the transmission systems</description>
<value>
<![CDATA[
SELECT ?name ?id WHERE {
 ?s r:type c:EquipmentContainer.
 ?s c:IdentifiedObject.name ?name.
 ?s c:IdentifiedObject.mRID ?id.
}
ORDER by ?name
]]>
</value>
</query>
<query>
<id>EMTSolar</id>
<keyfld>id</keyfld>
<description></description>
<value>
<![CDATA[
SELECT ?name ?bus ?ratedS ?ratedU ?maxP ?minP ?maxQ ?minQ ?ipu ?p ?q ?sysid ?id ?unitid ?cn1id WHERE {
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?s r:type c:PhotoVoltaicUnit.
 ?s c:IdentifiedObject.name ?name.
 ?s c:IdentifiedObject.mRID ?unitid.
 ?s c:PowerElectronicsUnit.maxP ?maxP.
 ?s c:PowerElectronicsUnit.minP ?minP.
 ?s c:PowerElectronicsUnit.PowerElectronicsConnection ?pec.
 ?pec c:Equipment.EquipmentContainer ?sys.
 ?pec c:PowerElectronicsConnection.ratedS ?ratedS.
 ?pec c:PowerElectronicsConnection.ratedU ?ratedU.
 ?pec c:PowerElectronicsConnection.maxIFault ?ipu.
 ?pec c:PowerElectronicsConnection.p ?p.
 ?pec c:PowerElectronicsConnection.q ?q.
 ?pec c:PowerElectronicsConnection.minQ ?minQ.
 ?pec c:PowerElectronicsConnection.maxQ ?maxQ.
 ?pec c:IdentifiedObject.mRID ?id.
 ?pec e:ConductingEquipment.FromConnectivityNode ?cn. 
 ?cn c:IdentifiedObject.mRID ?cn1id.
 ?cn c:IdentifiedObject.name ?bus
}
ORDER by ?name
]]>
</value>
</query>
<query>
<id>EMTWind</id>
<keyfld>id</keyfld>
<description></description>
<value>
<![CDATA[
SELECT ?name ?bus ?ratedS ?ratedU ?maxP ?minP ?maxQ ?minQ ?ipu ?p ?q ?sysid ?id ?unitid ?cn1id WHERE {
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?s r:type c:PowerElectronicsWindUnit.
 ?s c:IdentifiedObject.name ?name.
 ?s c:IdentifiedObject.mRID ?unitid.
 ?s c:PowerElectronicsUnit.maxP ?maxP.
 ?s c:PowerElectronicsUnit.minP ?minP.
 ?s c:PowerElectronicsUnit.PowerElectronicsConnection ?pec.
 ?pec c:Equipment.EquipmentContainer ?sys.
 ?pec c:PowerElectronicsConnection.ratedS ?ratedS.
 ?pec c:PowerElectronicsConnection.ratedU ?ratedU.
 ?pec c:PowerElectronicsConnection.maxIFault ?ipu.
 ?pec c:PowerElectronicsConnection.p ?p.
 ?pec c:PowerElectronicsConnection.q ?q.
 ?pec c:PowerElectronicsConnection.minQ ?minQ.
 ?pec c:PowerElectronicsConnection.maxQ ?maxQ.
 ?pec c:IdentifiedObject.mRID ?id.
 ?pec e:ConductingEquipment.FromConnectivityNode ?cn. 
 ?cn c:IdentifiedObject.mRID ?cn1id.
 ?cn c:IdentifiedObject.name ?bus
}
ORDER by ?name
]]>
</value>
</query>
<query>
<id>EMTExcST1A</id>
<keyfld>id</keyfld>
<description></description>
<value>
<![CDATA[
SELECT ?name ?ilr ?ka ?kc ?kf ?klr ?ta ?tb ?tb1 ?tc ?tc1 ?tf ?vamax ?vamin
?vimax ?vimin ?vrmax ?vrmin ?xe ?id ?eqid ?sysid WHERE {
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?s r:type c:ExcST1A.
 ?s c:ExcitationSystemDynamics.SynchronousMachineDynamics ?dyn.
 ?dyn c:SynchronousMachineDynamics.SynchronousMachine ?eq.
 ?eq c:Equipment.EquipmentContainer ?sys.
 ?eq c:IdentifiedObject.mRID ?eqid.
 ?s r:type c:ExcST1A.
 ?s c:IdentifiedObject.name ?name.
 ?s c:IdentifiedObject.mRID ?id.
 ?s c:ExcST1A.ilr ?ilr.
 ?s c:ExcST1A.ka ?ka.
 ?s c:ExcST1A.kc ?kc.
 ?s c:ExcST1A.kf ?kf.
 ?s c:ExcST1A.klr ?klr.
 ?s c:ExcST1A.ta ?ta.
 ?s c:ExcST1A.tb ?tb.
 ?s c:ExcST1A.tb1 ?tb1.
 ?s c:ExcST1A.tc ?tc.
 ?s c:ExcST1A.tc1 ?tc1.
 ?s c:ExcST1A.tf ?tf.
 ?s c:ExcST1A.vamax ?vamax.
 ?s c:ExcST1A.vamin ?vamin.
 ?s c:ExcST1A.vimax ?vimax.
 ?s c:ExcST1A.vimin ?vimin.
 ?s c:ExcST1A.vrmax ?vrmax.
 ?s c:ExcST1A.vrmin ?vrmin.
 ?s c:ExcST1A.xe ?xe.
} ORDER BY ?name
]]>
</value>
</query>
<query>
<id>EMTGovSteamSGO</id>
<keyfld>id</keyfld>
<description></description>
<value>
<![CDATA[
SELECT ?name ?k1 ?k2 ?k3 ?pmin ?pmax ?t1 ?t2 ?t3 ?t4 ?t5 ?t6 ?mwbase ?id ?eqid ?sysid
WHERE {
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?s r:type c:GovSteamSGO.
 ?s c:TurbineGovernorDynamics.SynchronousMachineDynamics ?dyn.
 ?dyn c:SynchronousMachineDynamics.SynchronousMachine ?eq.
 ?eq c:Equipment.EquipmentContainer ?sys.
 ?eq c:IdentifiedObject.mRID ?eqid.
 ?s r:type c:GovSteamSGO.
 ?s c:IdentifiedObject.name ?name.
 ?s c:IdentifiedObject.mRID ?id.
 ?s c:GovSteamSGO.k1 ?k1.
 ?s c:GovSteamSGO.k2 ?k2.
 ?s c:GovSteamSGO.k3 ?k3.
 ?s c:GovSteamSGO.pmin ?pmin.
 ?s c:GovSteamSGO.pmax ?pmax.
 ?s c:GovSteamSGO.t1 ?t1.
 ?s c:GovSteamSGO.t2 ?t2.
 ?s c:GovSteamSGO.t3 ?t3.
 ?s c:GovSteamSGO.t4 ?t4.
 ?s c:GovSteamSGO.t5 ?t5.
 ?s c:GovSteamSGO.t6 ?t6.
 ?s c:GovSteamSGO.mwbase ?mwbase.
} ORDER BY ?name
]]>
</value>
</query>
<query>
<id>EMTPssIEEE1A</id>
<keyfld>id</keyfld>
<description></description>
<value>
<![CDATA[
SELECT ?name ?a1 ?a2 ?inputSignalType ?ks ?t1 ?t2 ?t3 ?t4 ?t5 ?t6 ?vrmax ?vrmin
?id ?eqid ?sysid WHERE {
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?s r:type c:PssIEEE1A.
 ?s c:PowerSystemStabilizerDynamics.ExcitationSystemDynamics ?exc.
 ?exc c:ExcitationSystemDynamics.SynchronousMachineDynamics ?dyn.
 ?dyn c:SynchronousMachineDynamics.SynchronousMachine ?eq.
 ?eq c:Equipment.EquipmentContainer ?sys.
 ?eq c:IdentifiedObject.mRID ?eqid.
 ?s r:type c:PssIEEE1A.
 ?s c:IdentifiedObject.name ?name.
 ?s c:IdentifiedObject.mRID ?id.
 ?s c:PssIEEE1A.a1 ?a1.
 ?s c:PssIEEE1A.a2 ?a2.
 ?s c:PssIEEE1A.inputSignalType ?inputSignalRaw.
    bind(strafter(str(?inputSignalRaw),"InputSignalKind.") as ?inputSignalType)
 ?s c:PssIEEE1A.ks ?ks.
 ?s c:PssIEEE1A.t1 ?t1.
 ?s c:PssIEEE1A.t2 ?t2.
 ?s c:PssIEEE1A.t3 ?t3.
 ?s c:PssIEEE1A.t4 ?t4.
 ?s c:PssIEEE1A.t5 ?t5.
 ?s c:PssIEEE1A.t6 ?t6.
 ?s c:PssIEEE1A.vrmax ?vrmax.
 ?s c:PssIEEE1A.vrmin ?vrmin.
} ORDER BY ?name
]]>
</value>
</query>
<query>
<id>EMTWeccREGCA</id>
<keyfld>id</keyfld>
<description></description>
<value>
<![CDATA[
SELECT ?name ?brkpt ?iolim ?iqrmax ?iqrmin ?ivpl1 ?ivplsw ?ivpnt0 ?ivpnt1 ?khv
?rrpwr ?tfltr ?tg ?volim ?zerox ?id ?eqid ?sysid WHERE {
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?s r:type c:WeccREGCA.
 ?s c:WeccDynamics.PowerElectronicsConnection ?eq.
 ?eq c:Equipment.EquipmentContainer ?sys.
 ?eq c:IdentifiedObject.mRID ?eqid.
 ?s r:type c:WeccREGCA.
 ?s c:IdentifiedObject.name ?name.
 ?s c:IdentifiedObject.mRID ?id.
 ?s c:WeccREGCA.brkpt ?brkpt.
 ?s c:WeccREGCA.iolim ?iolim.
 ?s c:WeccREGCA.iqrmax ?iqrmax.
 ?s c:WeccREGCA.iqrmin ?iqrmin.
 ?s c:WeccREGCA.ivpl1 ?ivpl1.
 ?s c:WeccREGCA.ivplsw ?ivplsw.
 ?s c:WeccREGCA.ivpnt0 ?ivpnt0.
 ?s c:WeccREGCA.ivpnt1 ?ivpnt1.
 ?s c:WeccREGCA.khv ?khv.
 ?s c:WeccREGCA.rrpwr ?rrpwr.
 ?s c:WeccREGCA.tfltr ?tfltr.
 ?s c:WeccREGCA.tg ?tg.
 ?s c:WeccREGCA.volim ?volim.
 ?s c:WeccREGCA.zerox ?zerox.
} ORDER BY ?name
]]>
</value>
</query>
<query>
<id>EMTWeccREECA</id>
<keyfld>id</keyfld>
<description></description>
<value>
<![CDATA[
SELECT ?name ?db1 ?db2 ?dPmax ?dPmin ?imax ?iqfrz ?iqh1 ?iql1 ?kqi ?kqp ?kqv
?kvi ?kvp ?pfFlag ?pFlag ?pmax ?pmin ?pqFlag ?qFlag ?qmax ?qmin ?thld ?thld2
?tiq ?tp ?tpord ?trv ?vdi1i1 ?vdi1i2 ?vdi1i3 ?vdi1i4 ?vdi1v1 ?vdi1v2 ?vdi1v3
?vdi1v4 ?vdi2i1 ?vdi2i2 ?vdi2i3 ?vdi2i4 ?vdi2v1 ?vdi2v2 ?vdi2v3 ?vdi2v4 ?vdip
?vFlag ?vmax ?vmin ?vref0 ?vref1 ?vup ?id ?eqid ?sysid WHERE {
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?s r:type c:WeccREECA.
 ?s c:WeccDynamics.PowerElectronicsConnection ?eq.
 ?eq c:Equipment.EquipmentContainer ?sys.
 ?eq c:IdentifiedObject.mRID ?eqid.
 ?s r:type c:WeccREECA.
 ?s c:IdentifiedObject.name ?name.
 ?s c:IdentifiedObject.mRID ?id.
 ?s c:WeccREECA.db1 ?db1.
 ?s c:WeccREECA.db2 ?db2.
 ?s c:WeccREECA.dPmax ?dPmax.
 ?s c:WeccREECA.dPmin ?dPmin.
 ?s c:WeccREECA.imax ?imax.
 ?s c:WeccREECA.iqfrz ?iqfrz.
 ?s c:WeccREECA.iqh1 ?iqh1.
 ?s c:WeccREECA.iql1 ?iql1.
 ?s c:WeccREECA.kqi ?kqi.
 ?s c:WeccREECA.kqp ?kqp.
 ?s c:WeccREECA.kqv ?kqv.
 ?s c:WeccREECA.kvi ?kvi.
 ?s c:WeccREECA.kvp ?kvp.
 ?s c:WeccREECA.pfFlag ?pfFlag.
 ?s c:WeccREECA.pFlag ?pFlag.
 ?s c:WeccREECA.pmax ?pmax.
 ?s c:WeccREECA.pmin ?pmin.
 ?s c:WeccREECA.pqFlag ?pqFlag.
 ?s c:WeccREECA.qFlag ?qFlag.
 ?s c:WeccREECA.qmax ?qmax.
 ?s c:WeccREECA.qmin ?qmin.
 ?s c:WeccREECA.thld ?thld.
 ?s c:WeccREECA.thld2 ?thld2.
 ?s c:WeccREECA.tiq ?tiq.
 ?s c:WeccREECA.tp ?tp.
 ?s c:WeccREECA.tpord ?tpord.
 ?s c:WeccREECA.trv ?trv.
 ?s c:WeccREECA.vdi1i1 ?vdi1i1.
 ?s c:WeccREECA.vdi1i2 ?vdi1i2.
 ?s c:WeccREECA.vdi1i3 ?vdi1i3.
 ?s c:WeccREECA.vdi1i4 ?vdi1i4.
 ?s c:WeccREECA.vdi1v1 ?vdi1v1.
 ?s c:WeccREECA.vdi1v2 ?vdi1v2.
 ?s c:WeccREECA.vdi1v3 ?vdi1v3.
 ?s c:WeccREECA.vdi1v4 ?vdi1v4.
 ?s c:WeccREECA.vdi2i1 ?vdi2i1.
 ?s c:WeccREECA.vdi2i2 ?vdi2i2.
 ?s c:WeccREECA.vdi2i3 ?vdi2i3.
 ?s c:WeccREECA.vdi2i4 ?vdi2i4.
 ?s c:WeccREECA.vdi2v1 ?vdi2v1.
 ?s c:WeccREECA.vdi2v2 ?vdi2v2.
 ?s c:WeccREECA.vdi2v3 ?vdi2v3.
 ?s c:WeccREECA.vdi2v4 ?vdi2v4.
 ?s c:WeccREECA.vdip ?vdip.
 ?s c:WeccREECA.vFlag ?vFlag.
 ?s c:WeccREECA.vmax ?vmax.
 ?s c:WeccREECA.vmin ?vmin.
 ?s c:WeccREECA.vref0 ?vref0.
 ?s c:WeccREECA.vref1 ?vref1.
 ?s c:WeccREECA.vup ?vup.
} ORDER BY ?name
]]>
</value>
</query>
<query>
<id>EMTWeccREPCA</id>
<keyfld>id</keyfld>
<description></description>
<value>
<![CDATA[
SELECT ?name ?db ?ddn ?dup ?emax ?emin ?fdbd1 ?fdbd2 ?femax ?femin ?frqFlag ?kc
?ki ?kig ?kp ?kpg ?pmax ?pmin ?qmax ?qmin ?rc ?refFlag ?tfltr ?tft ?tfv ?tlag
?tp ?vcmpFlag ?vfrz ?xc ?id ?eqid ?sysid WHERE {
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?s r:type c:WeccREPCA.
 ?s c:WeccDynamics.PowerElectronicsConnection ?eq.
 ?eq c:Equipment.EquipmentContainer ?sys.
 ?eq c:IdentifiedObject.mRID ?eqid.
 ?s r:type c:WeccREPCA.
 ?s c:IdentifiedObject.name ?name.
 ?s c:IdentifiedObject.mRID ?id.
 ?s c:WeccREPCA.db ?db.
 ?s c:WeccREPCA.ddn ?ddn.
 ?s c:WeccREPCA.dup ?dup.
 ?s c:WeccREPCA.emax ?emax.
 ?s c:WeccREPCA.emin ?emin.
 ?s c:WeccREPCA.fdbd1 ?fdbd1.
 ?s c:WeccREPCA.fdbd2 ?fdbd2.
 ?s c:WeccREPCA.femax ?femax.
 ?s c:WeccREPCA.femin ?femin.
 ?s c:WeccREPCA.frqFlag ?frqFlag.
 ?s c:WeccREPCA.kc ?kc.
 ?s c:WeccREPCA.ki ?ki.
 ?s c:WeccREPCA.kig ?kig.
 ?s c:WeccREPCA.kp ?kp.
 ?s c:WeccREPCA.kpg ?kpg.
 ?s c:WeccREPCA.pmax ?pmax.
 ?s c:WeccREPCA.pmin ?pmin.
 ?s c:WeccREPCA.qmax ?qmax.
 ?s c:WeccREPCA.qmin ?qmin.
 ?s c:WeccREPCA.rc ?rc.
 ?s c:WeccREPCA.refFlag ?refFlag.
 ?s c:WeccREPCA.tfltr ?tfltr.
 ?s c:WeccREPCA.tft ?tft.
 ?s c:WeccREPCA.tfv ?tfv.
 ?s c:WeccREPCA.tlag ?tlag.
 ?s c:WeccREPCA.tp ?tp.
 ?s c:WeccREPCA.vcmpFlag ?vcmpFlag.
 ?s c:WeccREPCA.vfrz ?vfrz.
 ?s c:WeccREPCA.xc ?xc.
} ORDER BY ?name
]]>
</value>
</query>
<query>
<id>EMTWeccWTGTA</id>
<keyfld>id</keyfld>
<description></description>
<value>
<![CDATA[
SELECT ?name ?dshaft ?hg ?ht ?kshaft ?id ?eqid ?sysid WHERE {
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?s r:type c:WeccWTGTA.
 ?s c:WeccDynamics.PowerElectronicsConnection ?eq.
 ?eq c:Equipment.EquipmentContainer ?sys.
 ?eq c:IdentifiedObject.mRID ?eqid.
 ?s r:type c:WeccWTGTA.
 ?s c:IdentifiedObject.name ?name.
 ?s c:IdentifiedObject.mRID ?id.
 ?s c:WeccWTGTA.dshaft ?dshaft.
 ?s c:WeccWTGTA.hg ?hg.
 ?s c:WeccWTGTA.ht ?ht.
 ?s c:WeccWTGTA.kshaft ?kshaft.
} ORDER BY ?name
]]>
</value>
</query>
<query>
<id>EMTWeccWTGARA</id>
<keyfld>id</keyfld>
<description></description>
<value>
<![CDATA[
SELECT ?name ?ka ?t0 ?id ?eqid ?sysid WHERE {
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?s r:type c:WeccWTGARA.
 ?s c:WeccDynamics.PowerElectronicsConnection ?eq.
 ?eq c:Equipment.EquipmentContainer ?sys.
 ?eq c:IdentifiedObject.mRID ?eqid.
 ?s r:type c:WeccWTGARA.
 ?s c:IdentifiedObject.name ?name.
 ?s c:IdentifiedObject.mRID ?id.
 ?s c:WeccWTGARA.ka ?ka.
 ?s c:WeccWTGARA.t0 ?t0.
} ORDER BY ?name
]]>
</value>
</query>
<query>
<id>EMTBaseVoltage</id>
<keyfld>vnom</keyfld>
<description></description>
<value>
<![CDATA[
SELECT DISTINCT ?vnom WHERE {
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?s c:Equipment.EquipmentContainer ?sys.
 {?s c:ConductingEquipment.BaseVoltage ?lev.}
  UNION
 { ?end c:PowerTransformerEnd.PowerTransformer ?s.
   ?end c:TransformerEnd.BaseVoltage ?lev.}
 ?lev r:type c:BaseVoltage.
 ?lev c:BaseVoltage.nominalVoltage ?vstr.
  bind(xsd:string(?vstr) as ?xsdstr)
  bind(xsd:float(?xsdstr) as ?vnom)
}
ORDER BY ?vnom
]]>
</value>
</query>
<query>
<id>EMTCompShunt</id>
<keyfld>id</keyfld>
<description>Shunt reactor or capacitor</description>
<value>
<![CDATA[
SELECT ?name ?basev ?nomu ?gsection ?bsection ?sections ?maxsections ?bus ?id ?sysid ?cn1id WHERE {
 ?s r:type c:LinearShuntCompensator.
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?s c:Equipment.EquipmentContainer ?sys.
 ?s c:IdentifiedObject.name ?name.
 ?s c:ConductingEquipment.BaseVoltage ?bv.
 ?bv c:BaseVoltage.nominalVoltage ?basev.
 ?s c:ShuntCompensator.nomU ?nomu. 
 ?s c:LinearShuntCompensator.gPerSection ?gsection. 
 ?s c:LinearShuntCompensator.bPerSection ?bsection. 
 ?s c:ShuntCompensator.sections ?sections. 
 ?s c:ShuntCompensator.maximumSections ?maxsections. 
 ?s c:IdentifiedObject.mRID ?id. 
 ?s e:ConductingEquipment.FromConnectivityNode ?cn. 
 ?cn c:IdentifiedObject.mRID ?cn1id.
 ?cn c:IdentifiedObject.name ?bus
}
ORDER by ?name
]]>
</value>
</query>
<query>
<id>EMTLoad</id>
<keyfld>id</keyfld>
<description>Balanced on three phases</description>
<value>
<![CDATA[
SELECT ?name ?id ?bus ?basev ?p ?q ?pz ?qz ?pi ?qi ?pp ?qp ?pe ?qe ?sysid ?cn1id WHERE {
 ?s r:type c:EnergyConsumer.
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?s c:Equipment.EquipmentContainer ?sys.
 ?s c:IdentifiedObject.name ?name.
 ?s c:IdentifiedObject.mRID ?id.
 ?s c:ConductingEquipment.BaseVoltage ?bv.
 ?bv c:BaseVoltage.nominalVoltage ?basev.
 ?s c:EnergyConsumer.p ?p.
 ?s c:EnergyConsumer.q ?q.
 ?s c:EnergyConsumer.LoadResponse ?lr.
 ?lr c:LoadResponseCharacteristic.pConstantImpedance ?pz.
 ?lr c:LoadResponseCharacteristic.qConstantImpedance ?qz.
 ?lr c:LoadResponseCharacteristic.pConstantCurrent ?pi.
 ?lr c:LoadResponseCharacteristic.qConstantCurrent ?qi.
 ?lr c:LoadResponseCharacteristic.pConstantPower ?pp.
 ?lr c:LoadResponseCharacteristic.qConstantPower ?qp.
 OPTIONAL {?lr c:LoadResponseCharacteristic.pVoltageExponent ?pe.}
 OPTIONAL {?lr c:LoadResponseCharacteristic.qVoltageExponent ?qe.}
 ?s e:ConductingEquipment.FromConnectivityNode ?cn. 
 ?cn c:IdentifiedObject.mRID ?cn1id. 
 ?cn c:IdentifiedObject.name ?bus
}
ORDER by ?name
]]>
</value>
</query>
<query>
<id>EMTCountPowerXfmrWindings</id>
<keyfld>id</keyfld>
<description>for EMTPowerXfmrWinding</description>
<value>
<![CDATA[
SELECT ?id (count(?p) as ?count) WHERE {
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?p c:Equipment.EquipmentContainer ?sys.
 ?p r:type c:PowerTransformer.
 ?p c:IdentifiedObject.name ?id.
 ?end c:PowerTransformerEnd.PowerTransformer ?p.
}
GROUP BY ?id
ORDER BY ?id
]]>
</value>
</query>
<query>
<id>EMTXfmrLimit</id>
<keyfld>id</keyfld>
<description>operational limits for transformer windings</description>
<value>
<![CDATA[
SELECT DISTINCT ?id ?name ?endid ?tname ?value ?dur ?inf ?dir WHERE {
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?p c:Equipment.EquipmentContainer ?sys.
 ?end c:PowerTransformerEnd.PowerTransformer ?p.
 ?end c:IdentifiedObject.mRID ?endid.
 ?ols e:OperationalLimitSet.TransformerEnd ?end.
 ?s c:OperationalLimit.OperationalLimitSet ?ols.
 ?s c:OperationalLimit.OperationalLimitType ?olt.
 ?s r:type c:ApparentPowerLimit.
 ?s c:IdentifiedObject.mRID ?id.
 ?s c:IdentifiedObject.name ?name.
 ?s c:ApparentPowerLimit.value ?valstr.
  bind(xsd:string(?valstr) as ?xsdstr)
  bind(xsd:float(?xsdstr) as ?value)
 ?olt c:IdentifiedObject.name ?tname.
 ?olt c:OperationalLimitType.isInfiniteDuration ?inf.
 ?olt c:OperationalLimitType.direction ?rawdir.
  bind(strafter(str(?rawdir), "OperationalLimitDirectionKind.") as ?dir)
 OPTIONAL {?olt c:OperationalLimitType.acceptableDuration ?dur.}
}
ORDER BY ?endid ?inf ?tname ?inf ?value
]]>
</value>
</query>
<query>
<id>EMTBranchLimit</id>
<keyfld>id</keyfld>
<description>operational limits for conducting equipment</description>
<value>
<![CDATA[
SELECT DISTINCT ?id ?name ?eqid ?eqtype ?tname ?value ?dur ?inf ?dir WHERE {
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?eq c:Equipment.EquipmentContainer ?sys.
 ?eq c:IdentifiedObject.mRID ?eqid.
 ?eq r:type ?typeraw.
  bind(strafter(str(?typeraw),"#") as ?eqtype)
 ?ols e:OperationalLimitSet.ConductingEquipment ?eq.
 ?s c:OperationalLimit.OperationalLimitSet ?ols.
 ?s c:OperationalLimit.OperationalLimitType ?olt.
 ?s r:type c:ApparentPowerLimit.
 ?s c:IdentifiedObject.mRID ?id.
 ?s c:IdentifiedObject.name ?name.
 ?s c:ApparentPowerLimit.value ?valstr.
  bind(xsd:string(?valstr) as ?xsdstr)
  bind(xsd:float(?xsdstr) as ?value)
 ?olt c:IdentifiedObject.name ?tname.
 ?olt c:OperationalLimitType.isInfiniteDuration ?inf.
 ?olt c:OperationalLimitType.direction ?rawdir.
  bind(strafter(str(?rawdir), "OperationalLimitDirectionKind.") as ?dir)
 OPTIONAL {?olt c:OperationalLimitType.acceptableDuration ?dur.}
}
ORDER BY ?eqid ?inf ?tname ?inf ?value
]]>
</value>
</query>
<query>
<id>EMTBusVoltage</id>
<keyfld>cnid</keyfld>
<description>Bus voltages from power flow in a merged instance</description>
<value>
<![CDATA[
SELECT DISTINCT ?cnid ?name ?vmag ?vdeg WHERE {
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?s c:ConnectivityNode.ConnectivityNodeContainer ?sys.
 ?s c:IdentifiedObject.mRID ?cnid.
 ?s c:IdentifiedObject.name ?name.
 ?sv e:SvVoltage.ConnectivityNode ?s.
 ?sv c:SvVoltage.v ?vmag.
 ?sv c:SvVoltage.angle ?vdeg
} ORDER BY ?name
]]>
</value>
</query>
<query>
<id>EMTBranchFlow</id>
<keyfld>eqid</keyfld>
<description>Branch power flows in a merged instance</description>
<value>
<![CDATA[
SELECT DISTINCT ?eqid ?eqtype ?eqname ?p ?q WHERE {
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?s c:Equipment.EquipmentContainer ?sys.
 ?s c:IdentifiedObject.mRID ?eqid.
 ?s c:IdentifiedObject.name ?eqname.
 ?s r:type ?typeraw.
  bind(strafter(str(?typeraw),"#") as ?eqtype)
 ?sv e:SvPowerFlow.ConductingEquipment ?s.
 ?sv c:SvPowerFlow.p ?p.
 ?sv c:SvPowerFlow.q ?q
} ORDER BY ?eqtype ?eqname
]]>
</value>
</query>
<query>
<id>EMTXfmrFlow</id>
<keyfld>endid</keyfld>
<description>Transformer winding (end) power flows in a merged instance</description>
<value>
<![CDATA[
SELECT DISTINCT ?endid ?endtype ?endname ?p ?q WHERE {
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?pxf c:Equipment.EquipmentContainer ?sys.
 ?end c:PowerTransformerEnd.PowerTransformer ?pxf.
 ?end c:IdentifiedObject.mRID ?endid.
 ?end c:IdentifiedObject.name ?endname.
 ?end r:type ?typeraw.
  bind(strafter(str(?typeraw),"#") as ?endtype)
 ?sv e:SvPowerFlow.TransformerEnd ?end.
 ?sv c:SvPowerFlow.p ?p.
 ?sv c:SvPowerFlow.q ?q
} ORDER BY ?endtype ?endname
]]>
</value>
</query>
<query>
<id>EMTBusVoltageIC</id>
<keyfld>cnid</keyfld>
<description>Bus voltages from power flow in a standalone file. Don't select the system.</description>
<value>
<![CDATA[
SELECT DISTINCT ?cnid ?vmag ?vdeg WHERE {
 ?sv e:SvVoltage.ConnectivityNode ?cn.
  bind(substr(str(?cn),strlen(str(?cn))-35) as ?cnid)
 ?sv c:SvVoltage.v ?vmag.
 ?sv c:SvVoltage.angle ?vdeg
} ORDER BY ?cnid
]]>
</value>
</query>
<query>
<id>EMTBranchFlowIC</id>
<keyfld>eqid</keyfld>
<description>Branch power flows in a standalone file. Don't select the system.</description>
<value>
<![CDATA[
SELECT DISTINCT ?eqid ?p ?q WHERE {
 ?sv e:SvPowerFlow.ConductingEquipment ?eq.
  bind(substr(str(?eq),strlen(str(?eq))-35) as ?eqid)
 ?sv c:SvPowerFlow.p ?p.
 ?sv c:SvPowerFlow.q ?q
} ORDER BY ?eqid
]]>
</value>
</query>
<query>
<id>EMTXfmrFlowIC</id>
<keyfld>endid</keyfld>
<description>Transformer winding (end) power flows in a standalone file. Don't select the system.</description>
<value>
<![CDATA[
SELECT DISTINCT ?endid ?p ?q WHERE {
 ?sv e:SvPowerFlow.TransformerEnd ?end.
  bind(substr(str(?end),strlen(str(?end))-35) as ?endid)
 ?sv c:SvPowerFlow.p ?p.
 ?sv c:SvPowerFlow.q ?q
} ORDER BY ?endid
]]>
</value>
</query>
<query>
<id>EMTIBRPlant*</id>
<keyfld>id*</keyfld>
<description>Collection of equipment comprising an inverter-based resource plant, with plant-level attributes.</description>
<value>
<![CDATA[
SELECT DISTINCT ?id ?name ?eqid ?eqtype WHERE {
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?p r:type e:IBRPlant.
 ?p c:IdentifiedObject.name ?name.
 ?p c:IdentifiedObject.mRID ?id.
 ?p e:GeneratingPlant.Equipments ?eq.
 ?eq c:Equipment.EquipmentContainer ?sys.
 ?eq c:IdentifiedObject.mRID ?eqid.
 {?eq a ?rawtype.
  bind(strafter(str(?rawtype),"#") as ?eqtype)}
} ORDER by ?id ?eqtype ?eqid
]]>
</value>
</query>
<query>
<id>EMTRotatingMachinePlant*</id>
<keyfld>id*</keyfld>
<description>Collection of equipment comprising a conventional generating plant.</description>
<value>
<![CDATA[
SELECT DISTINCT ?id ?name ?eqid ?eqtype WHERE {
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?p r:type e:RotatingMachinePlant.
 ?p c:IdentifiedObject.name ?name.
 ?p c:IdentifiedObject.mRID ?id.
 ?p e:GeneratingPlant.Equipments ?eq.
 ?eq c:Equipment.EquipmentContainer ?sys.
 ?eq c:IdentifiedObject.mRID ?eqid.
 {?eq a ?rawtype.
  bind(strafter(str(?rawtype),"#") as ?eqtype)}
} ORDER by ?id ?eqtype ?eqid
]]>
</value>
</query>
<query>
<id>EMTExcIEEEDC1A</id>
<keyfld>id</keyfld>
<description></description>
<value>
<![CDATA[
SELECT ?name ?efd1 ?efd2 ?exclim ?ka ?ke ?kf ?seefd1 ?seefd2 ?ta ?tb ?tc ?te ?tf
?uelin ?vrmax ?vrmin ?id ?eqid ?sysid WHERE {
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?s r:type c:ExcIEEEDC1A.
 ?s c:ExcitationSystemDynamics.SynchronousMachineDynamics ?dyn.
 ?dyn c:SynchronousMachineDynamics.SynchronousMachine ?eq.
 ?eq c:Equipment.EquipmentContainer ?sys.
 ?eq c:IdentifiedObject.mRID ?eqid.
 ?s r:type c:ExcIEEEDC1A.
 ?s c:IdentifiedObject.name ?name.
 ?s c:IdentifiedObject.mRID ?id.
 ?s c:ExcIEEEDC1A.efd1 ?efd1.
 ?s c:ExcIEEEDC1A.efd2 ?efd2.
 ?s c:ExcIEEEDC1A.exclim ?exclim.
 ?s c:ExcIEEEDC1A.ka ?ka.
 ?s c:ExcIEEEDC1A.ke ?ke.
 ?s c:ExcIEEEDC1A.kf ?kf.
 ?s c:ExcIEEEDC1A.seefd1 ?seefd1.
 ?s c:ExcIEEEDC1A.seefd2 ?seefd2.
 ?s c:ExcIEEEDC1A.ta ?ta.
 ?s c:ExcIEEEDC1A.tb ?tb.
 ?s c:ExcIEEEDC1A.tc ?tc.
 ?s c:ExcIEEEDC1A.te ?te.
 ?s c:ExcIEEEDC1A.tf ?tf.
 ?s c:ExcIEEEDC1A.uelin ?uelin.
 ?s c:ExcIEEEDC1A.vrmax ?vrmax.
 ?s c:ExcIEEEDC1A.vrmin ?vrmin.
} ORDER BY ?name
]]>
</value>
</query>
<query>
<id>EMTExcSEXS</id>
<keyfld>id</keyfld>
<description></description>
<value>
<![CDATA[
SELECT ?name ?efdmax ?efdmin ?emax ?emin ?k ?kc ?tatb ?tb ?tc ?te ?id ?eqid
?sysid WHERE {
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?s r:type c:ExcSEXS.
 ?s c:ExcitationSystemDynamics.SynchronousMachineDynamics ?dyn.
 ?dyn c:SynchronousMachineDynamics.SynchronousMachine ?eq.
 ?eq c:Equipment.EquipmentContainer ?sys.
 ?eq c:IdentifiedObject.mRID ?eqid.
 ?s r:type c:ExcSEXS.
 ?s c:IdentifiedObject.name ?name.
 ?s c:IdentifiedObject.mRID ?id.
 ?s c:ExcSEXS.efdmax ?efdmax.
 ?s c:ExcSEXS.efdmin ?efdmin.
 ?s c:ExcSEXS.emax ?emax.
 ?s c:ExcSEXS.emin ?emin.
 ?s c:ExcSEXS.k ?k.
 ?s c:ExcSEXS.kc ?kc.
 ?s c:ExcSEXS.tatb ?tatb.
 ?s c:ExcSEXS.tb ?tb.
 ?s c:ExcSEXS.tc ?tc.
 ?s c:ExcSEXS.te ?te.
} ORDER BY ?name
]]>
</value>
</query>
<query>
<id>EMTGovGAST</id>
<keyfld>id</keyfld>
<description></description>
<value>
<![CDATA[
SELECT ?name ?at ?dturb ?kt ?mwbase ?r ?t1 ?t2 ?t3 ?vmax ?vmin ?id ?eqid ?sysid
WHERE {
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?s r:type c:GovGAST.
 ?s c:TurbineGovernorDynamics.SynchronousMachineDynamics ?dyn.
 ?dyn c:SynchronousMachineDynamics.SynchronousMachine ?eq.
 ?eq c:Equipment.EquipmentContainer ?sys.
 ?eq c:IdentifiedObject.mRID ?eqid.
 ?s r:type c:GovGAST.
 ?s c:IdentifiedObject.name ?name.
 ?s c:IdentifiedObject.mRID ?id.
 ?s c:GovGAST.at ?at.
 ?s c:GovGAST.dturb ?dturb.
 ?s c:GovGAST.kt ?kt.
 ?s c:GovGAST.mwbase ?mwbase.
 ?s c:GovGAST.r ?r.
 ?s c:GovGAST.t1 ?t1.
 ?s c:GovGAST.t2 ?t2.
 ?s c:GovGAST.t3 ?t3.
 ?s c:GovGAST.vmax ?vmax.
 ?s c:GovGAST.vmin ?vmin.
} ORDER BY ?name
]]>
</value>
</query>
<query>
<id>EMTGovHydro1</id>
<keyfld>id</keyfld>
<description></description>
<value>
<![CDATA[
SELECT ?name ?at ?dturb ?gmax ?gmin ?hdam ?mwbase ?qnl ?rperm ?rtemp ?tf ?tg ?tr
?tw ?velm ?id ?eqid ?sysid WHERE {
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?s r:type c:GovHydro1.
 ?s c:TurbineGovernorDynamics.SynchronousMachineDynamics ?dyn.
 ?dyn c:SynchronousMachineDynamics.SynchronousMachine ?eq.
 ?eq c:Equipment.EquipmentContainer ?sys.
 ?eq c:IdentifiedObject.mRID ?eqid.
 ?s r:type c:GovHydro1.
 ?s c:IdentifiedObject.name ?name.
 ?s c:IdentifiedObject.mRID ?id.
 ?s c:GovHydro1.at ?at.
 ?s c:GovHydro1.dturb ?dturb.
 ?s c:GovHydro1.gmax ?gmax.
 ?s c:GovHydro1.gmin ?gmin.
 ?s c:GovHydro1.hdam ?hdam.
 ?s c:GovHydro1.mwbase ?mwbase.
 ?s c:GovHydro1.qnl ?qnl.
 ?s c:GovHydro1.rperm ?rperm.
 ?s c:GovHydro1.rtemp ?rtemp.
 ?s c:GovHydro1.tf ?tf.
 ?s c:GovHydro1.tg ?tg.
 ?s c:GovHydro1.tr ?tr.
 ?s c:GovHydro1.tw ?tw.
 ?s c:GovHydro1.velm ?velm.
} ORDER BY ?name
]]>
</value>
</query>
<query>
<id>EMTGovSteam0</id>
<keyfld>id</keyfld>
<description></description>
<value>
<![CDATA[
SELECT ?name ?dt ?mwbase ?r ?t1 ?t2 ?t3 ?vmax ?vmin ?id ?eqid ?sysid WHERE {
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?s r:type c:GovSteam0.
 ?s c:TurbineGovernorDynamics.SynchronousMachineDynamics ?dyn.
 ?dyn c:SynchronousMachineDynamics.SynchronousMachine ?eq.
 ?eq c:Equipment.EquipmentContainer ?sys.
 ?eq c:IdentifiedObject.mRID ?eqid.
 ?s r:type c:GovSteam0.
 ?s c:IdentifiedObject.name ?name.
 ?s c:IdentifiedObject.mRID ?id.
 ?s c:GovSteam0.dt ?dt.
 ?s c:GovSteam0.mwbase ?mwbase.
 ?s c:GovSteam0.r ?r.
 ?s c:GovSteam0.t1 ?t1.
 ?s c:GovSteam0.t2 ?t2.
 ?s c:GovSteam0.t3 ?t3.
 ?s c:GovSteam0.vmax ?vmax.
 ?s c:GovSteam0.vmin ?vmin.
} ORDER BY ?name
]]>
</value>
</query>
<query>
<id>EMTPss1A</id>
<keyfld>id</keyfld>
<description></description>
<value>
<![CDATA[
SELECT ?name ?a1 ?a2 ?a3 ?a4 ?a5 ?a6 ?a7 ?a8 ?inputSignalType ?kd ?ks ?t1 ?t2
?t3 ?t4 ?t5 ?t6 ?tdelay ?vcl ?vcu ?vrmax ?vrmin ?id ?eqid ?sysid WHERE {
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?s r:type c:Pss1A.
 ?s c:PowerSystemStabilizerDynamics.ExcitationSystemDynamics ?exc.
 ?exc c:ExcitationSystemDynamics.SynchronousMachineDynamics ?dyn.
 ?dyn c:SynchronousMachineDynamics.SynchronousMachine ?eq.
 ?eq c:Equipment.EquipmentContainer ?sys.
 ?eq c:IdentifiedObject.mRID ?eqid.
 ?s r:type c:Pss1A.
 ?s c:IdentifiedObject.name ?name.
 ?s c:IdentifiedObject.mRID ?id.
 ?s c:Pss1A.a1 ?a1.
 ?s c:Pss1A.a2 ?a2.
 ?s c:Pss1A.a3 ?a3.
 ?s c:Pss1A.a4 ?a4.
 ?s c:Pss1A.a5 ?a5.
 ?s c:Pss1A.a6 ?a6.
 ?s c:Pss1A.a7 ?a7.
 ?s c:Pss1A.a8 ?a8.
 ?s c:Pss1A.inputSignalType ?inputSignalType.
 ?s c:Pss1A.kd ?kd.
 ?s c:Pss1A.ks ?ks.
 ?s c:Pss1A.t1 ?t1.
 ?s c:Pss1A.t2 ?t2.
 ?s c:Pss1A.t3 ?t3.
 ?s c:Pss1A.t4 ?t4.
 ?s c:Pss1A.t5 ?t5.
 ?s c:Pss1A.t6 ?t6.
 ?s c:Pss1A.tdelay ?tdelay.
 ?s c:Pss1A.vcl ?vcl.
 ?s c:Pss1A.vcu ?vcu.
 ?s c:Pss1A.vrmax ?vrmax.
 ?s c:Pss1A.vrmin ?vrmin.
} ORDER BY ?name
]]>
</value>
</query>
<query>
<id>EMTIEEECigreDLLParameters*</id>
<keyfld>id*</keyfld>
<description>List the values and sizes of parameters for the DLL interfaces.</description>
<value>
<![CDATA[
SELECT DISTINCT ?id ?seq ?kind ?val WHERE {
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?eq r:type c:PowerElectronicsConnection.
 ?eq c:Equipment.EquipmentContainer ?sys.
 ?d e:IEEECigreDLL.PowerElectronicsConnection ?eq.
 ?d c:IdentifiedObject.mRID ?id.
 ?p e:IEEECigreDLLParameter.IEEECigreDLL ?d.
 ?p e:IEEECigreDLLParameter.sequenceNumber ?seq.
 ?p e:IEEECigreDLLParameter.value ?val.
 {?p e:IEEECigreDLLParameter.parameterKind ?rawkind.
  bind(strafter(str(?rawkind),"Kind.") as ?kind)}
} ORDER by ?id ?seq
]]>
</value>
</query>
<query>
<id>EMTCountDLLParameters</id>
<keyfld>id</keyfld>
<description>Counts the number of parameters in each DLL interface.</description>
<value>
<![CDATA[
SELECT ?id (count(?p) as ?count) WHERE {
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?eq r:type c:PowerElectronicsConnection.
 ?eq c:Equipment.EquipmentContainer ?sys.
 ?d e:IEEECigreDLL.PowerElectronicsConnection ?eq.
 ?d c:IdentifiedObject.mRID ?id.
 ?p e:IEEECigreDLLParameter.IEEECigreDLL ?d.
}
GROUP BY ?id
ORDER BY ?id
]]>
</value>
</query>
<query>
<id>EMTIEEECigreDLL</id>
<keyfld>id</keyfld>
<description>DLL interface attached to an inverter.</description>
<value>
<![CDATA[
SELECT DISTINCT ?id ?name ?pecid ?datetime ?version ?emt ?rms ?dt ?uri ?snapshotUri ?vendor WHERE {
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?eq r:type c:PowerElectronicsConnection.
 ?eq c:Equipment.EquipmentContainer ?sys.
 ?d e:IEEECigreDLL.PowerElectronicsConnection ?eq.
 ?eq c:IdentifiedObject.mRID ?pecid.
 ?d c:IdentifiedObject.mRID ?id.
 ?d c:IdentifiedObject.name ?name.
 ?d e:IEEECigreDLL.firmwareInstalled ?datetime.
 ?d e:IEEECigreDLL.firmwareVersion ?version.
 ?d e:IEEECigreDLL.supportsEMT ?emt.
 ?d e:IEEECigreDLL.supportsRMS ?rms.
 ?d e:IEEECigreDLL.timestep ?dt.
 ?d e:IEEECigreDLL.uri ?uri.
 ?d e:IEEECigreDLL.snapshotUri ?snapshotUri.
 ?d e:IEEECigreDLL.vendorName ?vendor.
} ORDER by ?id
]]>
</value>
</query>
<query>
<id>EMTIBRPlantAttributes</id>
<keyfld>id</keyfld>
<description>IBR plant attributes, keyed on the inverter ID for convenience.</description>
<value>
<![CDATA[
SELECT DISTINCT ?id ?name ?plantid ?dcV ?dcCap ?acRgrid ?acLgrid ?acCap ?acRbridge ?acLbridge ?swtFreq ?filterKind ?dllid WHERE {
 ?sys c:IdentifiedObject.mRID ?sysid.
 ?p r:type e:IBRPlant.
 ?p c:IdentifiedObject.name ?name.
 ?p c:IdentifiedObject.mRID ?plantid.
 ?p e:GeneratingPlant.Equipments ?eq.
 ?eq r:type c:PowerElectronicsConnection.
 ?eq c:Equipment.EquipmentContainer ?sys.
 ?eq c:IdentifiedObject.mRID ?id.
 ?p e:IBRPlant.acFilterCapacitance ?acCap.
 ?p e:IBRPlant.acFilterRgrid ?acRgrid.
 ?p e:IBRPlant.acFilterLgrid ?acLgrid.
 ?p e:IBRPlant.acFilterRbridge ?acRbridge.
 ?p e:IBRPlant.acFilterLbridge ?acLbridge.
 ?p e:IBRPlant.dcLinkCapacitance ?dcCap.
 ?p e:IBRPlant.dcLinkVoltage ?dcV.
 ?p e:IBRPlant.switchingFrequency ?swtFreq.
 {?p e:IBRPlant.acFilterKind ?rawkind.
  bind(strafter(str(?rawkind),"Kind.") as ?filterKind)}
 OPTIONAL {
   ?d e:IEEECigreDLL.PowerElectronicsConnection ?eq.
   ?d c:IdentifiedObject.mRID ?dllid
   }
} ORDER by ?id
]]>
</value>
</query>
</queries>
